// スクリプト全体の共通変数等
#include "./lib_CommonDataManager.dnh"

let GAME_VERSION = "1.12";	//バージョン
let DEBUG_MODE = false;
let DEBUG_FILMING_MODE = false;	// 動画撮影用セッティング
let DEBUG_DIFFICULT = 3;	// テスト用難易度
let DEBUG_AUTO_TALK = 0;	// オート台詞送り(送り速度、0以下で無効)

// システムフォント
let sysBaseFont = "Verdana";
let sysBaseFontJP = "Meiryo";	//日本語

// ------ イベント番号 ------ //
//自機
let ev_ShootdownPlayer	= EV_USER_PLAYER + 101;	// 被弾イベント
let ev_ResetClip	= EV_USER_PLAYER + 102;	// STGサイズ変更

//ステージ
let ev_NextStageScene	= EV_USER_STAGE + 1;	// 汎用的なステージイベント
let ev_ResistBoss	= EV_USER_STAGE + 2;	// ボス登場イベント
let ev_CloseScript	= EV_USER_STAGE + 3;	// スクリプト強制終了
let ev_PluralClosed	= EV_USER_STAGE + 4;	// 連続再生終了イベント
let ev_SetScriptValue	= EV_USER_STAGE + 5;	// 値を送る
let ev_ResistBoss2	= EV_USER_STAGE + 6;	// ボス登場イベント（予備用）
let ev_ResistBoss3	= EV_USER_STAGE + 7;	// ボス登場イベント（予備用）

//システム
let ev_CallSound	= EV_USER_SYSTEM + 1;	// 効果音通知
let ev_CallItem		= EV_USER_SYSTEM + 2;	// アイテム配置
let ev_CallItemEx	= EV_USER_SYSTEM + 3;	// アイテム配置
let ev_BulletEffect	= EV_USER_SYSTEM + 4;	// 弾消しエフェクト
let ev_CallSoundEx	= EV_USER_SYSTEM + 5;	// 効果音通知（ボリューム設定付き）
let ev_ChapterStart	= EV_USER_SYSTEM + 6;	// チャプター開始通知
let ev_ChapterReset	= EV_USER_SYSTEM + 7;	// チャプターリセット通知
let ev_EnemyKilled	= EV_USER_SYSTEM + 8;	// 敵撃破通知
let ev_GetItem		= EV_USER_SYSTEM + 9;	// アイテム取得
let ev_StartSpell	= EV_USER_SYSTEM + 10;	// スペカ開始

//パッケージ用
let ev_Title_CallSound		= EV_USER_PACKAGE + 1;	// 効果音通知
let ev_Title_ResetVolume	= EV_USER_PACKAGE + 2;	// 音量リセット

// ------ 定数 ------ //
// ボスアニメ等のキャラクター判別用定数
let BOSS_ID_RANRAN	= "Ranran";
let BOSS_ID_CHII	= "Chii";
let BOSS_ID_HIKARIKO	= "Hikariko";
let BOSS_ID_SIRAN	= "Siran";
let BOSS_ID_MYSTIA	= "Mystia";
let BOSS_ID_NEBET	= "Nebet";
let BOSS_ID_SET		= "Set";
let BOSS_ID_HOROU	= "Horou";
let BOSS_ID_MUMEI	= "Mumei";
let BOSS_ID_MUMEI2	= "Mumei2";

// こちらは主に会話用
let BOSS_ID_REIMU	= "Reimu";
let BOSS_ID_MARISA	= "Marisa";
let BOSS_ID_SAKUYA	= "Sakuya";
let BOSS_ID_KAGUYA	= "Kaguya";
let BOSS_ID_DUMMY	= "Dummy";

//プレイヤー
// GetPlayerTypeの返り値
let PLAYER_REIMU_A	= 0;
let PLAYER_REIMU_B	= 1;
let PLAYER_REIMU_C	= 2;
let PLAYER_MARISA_A	= 3;
let PLAYER_MARISA_B	= 4;
let PLAYER_MARISA_C	= 5;
let PLAYER_SAKUYA_A	= 6;
let PLAYER_SAKUYA_B	= 7;
let PLAYER_SAKUYA_C	= 8;
let PLAYER_KAGUYA_A	= 9;
let PLAYER_KAGUYA_B	= 10;
let PLAYER_KAGUYA_C	= 11;
let PLAYER_TYPE_MAX	= 3;
let PLAYER_MAX	= 12;

//霊撃タイプ
// GetPlayerWeaponの返り値
let WEAPON_RED = 0;
let WEAPON_PURPLE = 1;
let WEAPON_BLUE = 2;
let WEAPON_SKY = 3;
let WEAPON_GREEN = 4;
let WEAPON_YELLOW = 5;
let WEAPON_ORANGE = 6;
let WEAPON_RAINBOW = 7;
let WEAPON_BLACK = 8;
let WEAPON_WHITE = 9;
let WEAPON_MAX = 10;

//難易度
let D_EASY	= 0;
let D_NORM	= 1;
let D_HARD	= 2;
let D_LUNA	= 3;
let D_DRIVE	= 5;
let D_LAST	= 6;
let D_EXTRA	= 10;

//Ex難易度
let D_EX_NORM	= 0;
let D_EX_EASY	= 1;
let D_EX_HARD	= 2;

//ステージ
let STAGE_NAME = ["Stage1", "Stage2", "Stage3", "Stage4", "Stage5", "Stage6", "Extra"];
let STAGE_MAX = 7;
let STAGE_EXTRA = 7;	//特殊ステージ

//プレイ状態
// GetPlayModeの返り値
let GAME_NONE	= 0;	//パッケージを介さずに直接起動
let GAME_MAIN	= 1;	//通しプレイ
let GAME_PRACTICE = 2;	//ステージプラクティス
let GAME_SPELL	= 3;	//スペルプラクティス
let GAME_DEBUG	= 4;

//システムスクリプト用
let MY_RESULT_CONTINUE = 777;


// ================================ 汎用関数群
// 基本的にtrueを返す
function IsGame() { return(GetAreaCommonData(CAREA_SYSTEM, "IS_GAME", true)); }

//rand_int
function rand_int(min, max) { return(trunc(rand(min, max+1))); }
//-1か1を返す。
function rand_dir { return(trunc(rand(0,2))*2-1); }

//0〜1の間を曲線で補完する
function GetCurve(val) { return(sin(val*90)); }		// 減衰
function GetCurve_d(val) { return(GetCurve(val)); }
function GetCurve_a(val) { return(1-cos(val*90)); }	// 加速
function GetCurve_ad(val) { return((1-cos(val*180))/2); }	// 加速〜減衰
function GetCurve_z(val) { return(sin(val*180)); }	// 0→1→0のsinカーブ

// ジグザグな感じの値を返す(0〜maxの範囲)
function GetZigZag(val, max)
{
	let tmp = val%(max*2);
	if (tmp <= max) { return(tmp); }
	else { return(max*2-tmp); }
}

//コモンデータ弄るやつ
function AddAreaCommonData(carea, cname, add_val) { SetAreaCommonData(carea,cname,GetAreaCommonData(carea,cname,0)+add_val); }

//えへくと用乱数　リプレイずれ防止の為、ONOFFできる部分の乱数にはコレを使うこと！
function randEffect(min, max)
{
	let tmp = GetAreaCommonData(CAREA_SYSTEM, "R_EFF", 1);
	tmp = (tmp*214013+2531011)%4294967296;
	SetAreaCommonData(CAREA_SYSTEM, "R_EFF", tmp);
	if (max==min) { return(min); }
	else { return(min+trunc(tmp/256)/16777216*(max-min)); }
}

//　2点間の距離を取得
function GetDistance( pos1, pos2 ) { return ( (pos2[1]-pos1[1])^2 + (pos2[0]-pos1[0])^2 ) ^ 0.5; }
//　pos1 から見た pos2 への角度を取得
function GetGapAngle( pos1, pos2 ) { return atan2( pos2[1]-pos1[1], pos2[0]-pos1[0] ); }

// 2D座標の回転
function Rotate2D_X(x, y, angle) { return( x*cos(angle) - y*sin(angle) ); }
function Rotate2D_Y(x, y, angle) { return( x*sin(angle) + y*cos(angle) ); }
function Rotate2D_XY(x, y, angle)
{
	let s = sin(angle);
	let c = cos(angle);
	return( [x*c-y*s, x*s+y*c] );
}

//6頂点の座標を追加
function AddImg_Triangle01(obj, base_vc,
		x1, y1, x2, y2,		//切り取り範囲
		to_x1, to_y1, to_x2, to_y2	//貼り付け範囲
	)
{
//	let base_vc = ObjPrim_GetVertexCount(obj);
//	ObjPrim_SetVertexCount(obj, base_vc+6);		//頂点を追加
	
	ObjPrim_SetVertexUVT(obj, base_vc+0, x1, y1);
	ObjPrim_SetVertexUVT(obj, base_vc+1, x1, y2);
	ObjPrim_SetVertexUVT(obj, base_vc+2, x2, y1);
	ObjPrim_SetVertexUVT(obj, base_vc+3, x2, y1);
	ObjPrim_SetVertexUVT(obj, base_vc+4, x1, y2);
	ObjPrim_SetVertexUVT(obj, base_vc+5, x2, y2);

	ObjPrim_SetVertexPosition(obj, base_vc+0, to_x1, to_y1, 0);
	ObjPrim_SetVertexPosition(obj, base_vc+1, to_x1, to_y2, 0);
	ObjPrim_SetVertexPosition(obj, base_vc+2, to_x2, to_y1, 0);
	ObjPrim_SetVertexPosition(obj, base_vc+3, to_x2, to_y1, 0);
	ObjPrim_SetVertexPosition(obj, base_vc+4, to_x1, to_y2, 0);
	ObjPrim_SetVertexPosition(obj, base_vc+5, to_x2, to_y2, 0);
}

//01の、貼り付け範囲の4点指定版
function AddImg_Triangle02(obj, base_vc,
		x1, y1, x2, y2,		//切り取り範囲
		to_x1, to_y1, to_x2, to_y2, to_x3, to_y3, to_x4, to_y4	//貼り付け範囲
	)
{
//	let base_vc = ObjPrim_GetVertexCount(obj);
//	ObjPrim_SetVertexCount(obj, base_vc+6);		//頂点を追加
	
	ObjPrim_SetVertexUVT(obj, base_vc+0, x1, y1);
	ObjPrim_SetVertexUVT(obj, base_vc+1, x1, y2);
	ObjPrim_SetVertexUVT(obj, base_vc+2, x2, y1);
	ObjPrim_SetVertexUVT(obj, base_vc+3, x2, y1);
	ObjPrim_SetVertexUVT(obj, base_vc+4, x1, y2);
	ObjPrim_SetVertexUVT(obj, base_vc+5, x2, y2);

	ObjPrim_SetVertexPosition(obj, base_vc+0, to_x1, to_y1, 0);
	ObjPrim_SetVertexPosition(obj, base_vc+1, to_x2, to_y2, 0);
	ObjPrim_SetVertexPosition(obj, base_vc+2, to_x3, to_y3, 0);
	ObjPrim_SetVertexPosition(obj, base_vc+3, to_x3, to_y3, 0);
	ObjPrim_SetVertexPosition(obj, base_vc+4, to_x2, to_y2, 0);
	ObjPrim_SetVertexPosition(obj, base_vc+5, to_x4, to_y4, 0);
}

// ================================ Yuke-Mode

function SetStgFrameDefault() { SetYukeMode(32, 16, 416, 464); }
function SetYukeMode(x1, y1, x2, y2)
{
	if (x1 == 32 && y1 == 16 && x2 == 416 && y2 == 464) {
		SetAreaCommonData(CAREA_SPELL, "WINDOW_CHANGED", 0);
	} else {
		SetAreaCommonData(CAREA_SPELL, "WINDOW_CHANGED", 1);
	}
	SetAreaCommonData(CAREA_SYSTEM, "WIN_X1", x1);
	SetAreaCommonData(CAREA_SYSTEM, "WIN_Y1", y1);
	SetAreaCommonData(CAREA_SYSTEM, "WIN_X2", x2);
	SetAreaCommonData(CAREA_SYSTEM, "WIN_Y2", y2);
	NotifyEventAll(ev_ResetClip, [x1,y1,x2,y2])
}
// フレーム枠のオブジェクトを取得（操作してもいいけど消さないでね！）
function GetStgFrameObject() { return(GetAreaCommonData(CAREA_SYSTEM, "STG_FRAME", -1)); }
// 定数群
function GetClipMinX() { return(GetAreaCommonData(CAREA_SYSTEM, "WIN_X1", 32)); }
function GetClipMinY() { return(GetAreaCommonData(CAREA_SYSTEM, "WIN_Y1", 16)); }
function GetClipMaxX() { return(GetAreaCommonData(CAREA_SYSTEM, "WIN_X2", 416)); }
function GetClipMaxY() { return(GetAreaCommonData(CAREA_SYSTEM, "WIN_Y2", 464)); }
function GetCenterX() { return((GetClipMinX()+GetClipMaxX())/2); }
function GetCenterY() { return((GetClipMinY()+GetClipMaxY())/2); }
function MinX() { return(GetClipMinX()); }
function MaxX() { return(GetClipMaxX()); }
function MinY() { return(GetClipMinY()); }
function MaxY() { return(GetClipMaxY()); }
function CenX() { return(GetCenterX()); }
function CenY() { return(GetCenterY()); }
function GetClipWidth() { return(GetClipMaxX()-GetClipMinX()); }
function GetClipHeight() { return(GetClipMaxY()-GetClipMinY()); }

//  ================================ wait関数群
//待機
function wait(n) { loop(n) { while(IsTimeStop) { yield; } yield; } }
//待機（敵オブジェクトが関わる場合）
function waitObj(obj, n)
{
	loop(n)
	{
		while(IsTimeStopObj(obj)) { yield; }
		yield;
	}
}
//待機（時間停止に影響されない）
function waitEx(n) { loop(n) { yield; } }

// ================================ 時間停止フラグ
function IsTimeStop() { return(GetAreaCommonData(CAREA_SYSTEM, "TIME_STOP", 0)); }
function IsTimeStopObj(obj) { return(GetAreaCommonData(CAREA_SYSTEM, "TIME_STOP", 0) || Obj_GetValueD(obj, "TIME_STOP", 0) > 0); }	// オブジェクト単体の時間停止
// 時間停止フラグ、value=停止フレーム
function SetTimeStopFlag(value) { SetAreaCommonData(CAREA_SYSTEM, "TIME_STOP", value); }
function SetTimeStopFlagObj(obj, value) { Obj_SetValue(obj, "TIME_STOP", max(Obj_GetValueD(obj, "TIME_STOP", 0), value)); }

// ================================ システム汎用関数
//コンフィグ設定
function GetMusicVolume() { return(GetAreaCommonData(CAREA_CONFIG, "BGM_Volume", 100) * GetAreaCommonData(CAREA_SYSTEM, "BGM_Volume_Multiply", 1)); }
function GetMusicVolume2() { return(GetAreaCommonData(CAREA_CONFIG, "BGM_Volume", 100) * GetAreaCommonData(CAREA_SYSTEM, "BGM_Volume_Multiply2", 1)); }
function GetSoundVolume() { return(GetAreaCommonData(CAREA_CONFIG, "SE_Volume", 100)); }
function GetPlayerSoundVolume() { return(GetAreaCommonData(CAREA_CONFIG, "Player_Volume", 100)); }
function GetRebirthFlag() { return(GetAreaCommonData(CAREA_STAGE, "Rebirth", 0)); }
function IsDrawBackground() { return(GetAreaCommonData(CAREA_CONFIG, "Background", 2)); }
function IsDrawEffect() { return(GetAreaCommonData(CAREA_CONFIG, "Effect", 3)); }
function IsChapterAlert() { return(GetAreaCommonData(CAREA_CONFIG, "Alert", 1)); }
function IsQuickRestart() { return(GetAreaCommonData(CAREA_CONFIG, "QRestart", 0)); }
function CfgIsUseShader() { return(GetAreaCommonData(CAREA_CONFIG, "IsUseShader", 1)); }
function CfgLaunchWindowSizeX() { return(GetAreaCommonData(CAREA_CONFIG, "LaunchWindowSizeX", 640)); }
function CfgLaunchWindowSizeY() { return(GetAreaCommonData(CAREA_CONFIG, "LaunchWindowSizeY", 480)); }
//オート台詞送り
function GetAutoTalk() { return(GetAreaCommonData(CAREA_SYSTEM, "Auto_Talk", DEBUG_AUTO_TALK)); }
//プレイモード取得
function GetPlayMode() { return(GetAreaCommonData(CAREA_STAGE, "PlayMode", GAME_NONE)); }
//難易度取得
function Difficult() { return(GetAreaCommonData(CAREA_STAGE, "Difficult", DEBUG_DIFFICULT)); }
function DifficultEx() { return(GetAreaCommonData(CAREA_STAGE, "DifficultEx", D_EX_EASY)); }	// Exステージ用
//コンティニュー
function GetContinueCount() { return(GetAreaCommonData(CAREA_SYSTEM, "CONTINUE", 0 )); }
function SetContinueRest(val) { SetAreaCommonData(CAREA_SYSTEM, "CONTINUE_REST", val ); }
function GetContinueRest() { return(GetAreaCommonData(CAREA_SYSTEM, "CONTINUE_REST", 0 )); }
// ステージindex取得
function GetStageIndex() { return(GetAreaCommonData(CAREA_STAGE, "StageIndex", 1 )); }
// 被弾数
function GetAllDeathCount() { return(GetAreaCommonData(CAREA_SYSTEM, "AllDeathCount", 0)); }
//最大得点
function GetMaxScore() { return(trunc(GetAreaCommonData(CAREA_SYSTEM, "MAX_SCORE", 10000 )/10)*10); }
function AddMaxScore(num) { return(SetAreaCommonData(CAREA_SYSTEM, "MAX_SCORE", GetAreaCommonData(CAREA_SYSTEM, "MAX_SCORE",10000) + num )); }
// エクステンドゲージ（スコア）
function GetExtendScore() { return(GetAreaCommonData(CAREA_SYSTEM, "EXTEND_SCORE", 0)); }
function SetExtendScore(num) { SetAreaCommonData(CAREA_SYSTEM, "EXTEND_SCORE", num); }
function AddExtendScore(num) { SetExtendScore(GetExtendScore()+num); }
function GetExtendMax() { return(GetAreaCommonData(CAREA_SYSTEM, "EXTEND_MAX", 1)); }
function SetExtendMax(num) { SetAreaCommonData(CAREA_SYSTEM, "EXTEND_MAX", num); }
// 時間結晶ゲージ（ボム）
function GetTimeItem() { return(GetAreaCommonData(CAREA_SYSTEM, "TIME_ITEM_COUNT", 0)); }
function SetTimeItem(num) { SetAreaCommonData(CAREA_SYSTEM, "TIME_ITEM_COUNT", num); }
function AddTimeItem(num)
{
	if (!GetItemLegacyMode() || num < 0) {
		SetTimeItem(GetTimeItem()+num);
	} else {
		let add_num = num * GetTimeItemMax() / GetItemLegacyMax();
		SetTimeItem(GetTimeItem()+add_num);
	}
}
function GetTimeItemMax() { return(GetAreaCommonData(CAREA_SYSTEM, "TIME_ITEM_MAX", 1)); }
function SetTimeItemMax(num) { SetAreaCommonData(CAREA_SYSTEM, "TIME_ITEM_MAX", num); }
// Zボムゲージ
function GetZSpellItem() { return(GetAreaCommonData(CAREA_SYSTEM, "Z_ITEM_COUNT", 0)); }
function SetZSpellItem(num) { SetAreaCommonData(CAREA_SYSTEM, "Z_ITEM_COUNT", num); }
function AddZSpellItem(num) { SetZSpellItem(min(GetZSpellItemMax(), GetZSpellItem()+num)); }
function GetZSpellItemMax() { return(GetAreaCommonData(CAREA_SYSTEM, "Z_ITEM_MAX", 1)); }
function SetZSpellItemMax(num) { SetAreaCommonData(CAREA_SYSTEM, "Z_ITEM_MAX", num); }
//最初のステージかな？
function IsFirstStage() { return(GetAreaCommonData(CAREA_SYSTEM, "FirstStage", true)); }
// クリアフラグ取得
function IsGameClear() { return(GetAreaCommonData(CAREA_STAGE, "GAME_CLEAR", 0 )); }
// ステージチャプター用
function GetStageChapter_SafeRate()
{
	let all = GetAreaCommonData(CAREA_STAGE, "STAGE_CHAPTER_ALL", 0);
	let safe = GetAreaCommonData(CAREA_STAGE, "STAGE_CHAPTER_SAFE", 0);
	let rate = 1;
	if (all > 0) { rate = safe/all; }
	return(rate);
}
function GetLastSpellEnable()
{
	if (Difficult == D_EXTRA) { return(GetStageChapter_SafeRate() >= 0.9); }
	else { return(GetStageChapter_SafeRate() >= 0.8); }
}


// ================================ プレイヤー情報
function flgItemAutoCollect() { return(GetAreaCommonData(CAREA_SYSTEM, "COLLECT_FLG", false )); }
function GetPlayerAutoItemCollectLine() { return(GetAreaCommonData(CAREA_SYSTEM, "PLAYER_COLLECT_L", 100 )); }
function GetPlayerItemCollectRange() { return(GetAreaCommonData(CAREA_SYSTEM, "PLAYER_COLLECT_R", 48 )); }
function GetPlayerInvincibility() { return(GetAreaCommonData(CAREA_SYSTEM, "PlayerInvincibility", 0 )); }
function GetItemLegacyMode() { return(GetAreaCommonData(CAREA_SYSTEM, "ITEM_LEGACY", false )); }
function GetItemLegacyMax() { return(GetAreaCommonData(CAREA_SYSTEM, "ITEM_LEGACY_MAX", 1 )); }
function SetItemCollectFlag(flg) { SetAreaCommonData(CAREA_SYSTEM, "COLLECT_FLG", flg ); }
task SetItemCollect(timer)
{
	SetItemCollectFlag(true);
	waitEx(timer);
	SetItemCollectFlag(false);
}
// ショット＆オプション向き
// ステージ中で変更する場合は、ショット禁止と併用すること（レーザーをリセットするため）
function GetPlayerAngle() { return(GetAreaCommonData(CAREA_SYSTEM, "PLAYER_ANGLE", 0 )); }
function SetPlayerAngle(angle) { SetAreaCommonData(CAREA_SYSTEM, "PLAYER_ANGLE", angle ); }

//プレイヤーは誰？
function GetPlayerType()
{
	alternative(GetPlayerReplayName())
	case("HSOB_PL01")  { return(PLAYER_REIMU_A); }
	case("HSOB_PL02") { return(PLAYER_MARISA_A); }
	case("HSOB_PL03") { return(PLAYER_SAKUYA_A); }
	case("HSOB_PL04") { return(PLAYER_KAGUYA_A); }
	others { return(-1); }
}
// 機体タイプも知りたい場合はこっち（今回は関係なし）
function GetPlayerTypeEx()
{
	alternative(GetPlayerReplayName())
	case("HSOB_PL01")  { return(PLAYER_REIMU_A); }
	case("HSOB_PL02") { return(PLAYER_MARISA_A); }
	case("HSOB_PL03") { return(PLAYER_SAKUYA_A); }
	case("HSOB_PL04") { return(PLAYER_KAGUYA_A); }
	others { return(-1); }
}
// 霊撃タイプを取得
function GetPlayerWeapon()
{
	return(GetAreaCommonData(CAREA_SYSTEM, "PlayerType", -1));
}
function GetPlayerRealWeapon()
{
	let wp_base = GetAreaCommonData(CAREA_SYSTEM, "PlayerType_Base", -1);
	let wp = GetAreaCommonData(CAREA_SYSTEM, "PlayerType", -1);
	if (wp_base == WEAPON_RAINBOW) { return(wp_base); }
	else { return(wp); }
}
function GetAllPlayerSubWeapon()
{
	return(GetAreaCommonData(CAREA_SYSTEM, "PlayerSubWeapon", []));
}
function IsRainbow() { return(GetPlayerRealWeapon() == WEAPON_RAINBOW); }



// ================================ 効果音関連
// 効果音ID
let sndTtSelect = 0;
let sndTtCancel = 1;
let sndTtDecide = 2;
let sndTtBoonUp = 3;
let sndTtBoonDown = 4;
let sndTtPause = 5;
let sndTtExtend2 = 6;
let sndTtTaiko01 = 7;
let sndTtTaiko02 = 8;
let sndTtScore = 9;
let sndTtCallSpellCard = 10;
let sndTtDanger = 11;
// 外部から効果音呼び出し
function CallSoundPackage(sndID)
{
	NotifyEventAll(ev_Title_CallSound, sndID);
}
function GetSoundScriptID()
{
	return(GetAreaCommonData(CAREA_TITLE, "SCRIPT_SOUND", -1));
}

// ================================ 楽曲関連
task PlayMusic( int_play_num, int_fade_out, int_delay )
{
	SetAreaCommonData(CAREA_TITLE, "Music_ID", int_play_num);
	SetAreaCommonData(CAREA_TITLE, "Music_Fadeout", int_fade_out);
	SetAreaCommonData(CAREA_TITLE, "Music_Delay", int_delay);
	loop(3) { yield; }
	SetAreaCommonData(CAREA_TITLE, "Music_Stop", false);
}
task PlaySubMusic( int_play_num, int_fade_out, int_delay )
{
	SetAreaCommonData(CAREA_TITLE, "Music_ID2", int_play_num);
	SetAreaCommonData(CAREA_TITLE, "Music_Fadeout", int_fade_out);
	SetAreaCommonData(CAREA_TITLE, "Music_Delay", int_delay);
	loop(3) { yield; }
	SetAreaCommonData(CAREA_TITLE, "Music_Stop", false);
}
task StopMusic()
{
	SetAreaCommonData(CAREA_TITLE, "Music_Stop", true);
}
task RestartMusic()
{
	SetAreaCommonData(CAREA_TITLE, "Music_Stop", false);
}
task DeleteMusic()
{
	SetAreaCommonData(CAREA_TITLE, "Music_ID", -1);
}
//曲の音量倍率（倍率）
function SetMusicVolumeMultiply(val) { SetAreaCommonData(CAREA_SYSTEM, "BGM_Volume_Multiply", val); }
function SetMusicVolumeMultiply2(val) { SetAreaCommonData(CAREA_SYSTEM, "BGM_Volume_Multiply2", val); }


// ================================ キーチェック関連
//キーリセットまで待機
function WaitForResetKey(value)
{
	while(GetVirtualKeyState(value) != KEY_FREE) { yield; }
	SetAreaCommonData(CAREA_TITLE, "MenuKey", -1);
	SetAreaCommonData(CAREA_TITLE, "MenuHold", 0); 
}
function WaitForResetKeyEx(values)
{
	loop
	{
		let flg = false;
		ascent(i in 0 .. length(values))
		{
			if (GetVirtualKeyState(values[i]) != KEY_FREE) { flg = true; break; }
		}
		if (!flg) { break; }
		yield;
	}
	SetAreaCommonData(CAREA_TITLE, "MenuKey", -1);
	SetAreaCommonData(CAREA_TITLE, "MenuHold", 0); 
}
//キーチェック  押しっぱなし時のみディレイ付き
//check_keys=感知キーを配列で
function CheckKey_Menu(key_delay, check_keys)
{
//	let tmp_flag = Menu_ReDrawFlag; //強制再描画フラグ
	let key_count = length(check_keys);
	let key_value = -1;
	let t = 0;
	while(key_value == -1)
	{
	//	if (Menu_ReDrawFlag != tmp_flag) { break; }
		if (t >= key_delay)
		{
			ascent(i in 0 .. key_count)
			{
				if (GetVirtualKeyState(check_keys[i]) == KEY_HOLD) { key_value = check_keys[i]; break; }
			}
		}
		ascent(i in 0 .. key_count)
		{
			if (GetVirtualKeyState(check_keys[i]) == KEY_PUSH) { key_value = check_keys[i]; break; }
		}
		t++;
		yield;
	}
	SetAreaCommonData(CAREA_TITLE, "MenuKey", -1);
	SetAreaCommonData(CAREA_TITLE, "MenuHold", 0); 
	return(key_value);
}
// 押しっぱなしで加速するタイプ
function CheckKey_MenuEx(key_delay, key_delay_short, check_keys)
{
	let key_count = length(check_keys);

//	let tmp_flag = Menu_ReDrawFlag; //強制再描画フラグ
	let before_key = GetAreaCommonData(CAREA_TITLE, "MenuKey", -1);
	let before_hold = GetAreaCommonData(CAREA_TITLE, "MenuHold", 0);
	let check_same = false;
	// 選択中に前回押したキーが含まれているか？
	ascent(i in 0 .. key_count)
	{
		if (check_keys[i] == before_key) { check_same = true; break; }
	}
	// 2回目以降は加速
	if (before_hold < 1) { key_delay_short = key_delay; }
	
	let key_value = -1;
	let t = 0;
	while(key_value == -1)
	{
	//	if (Menu_ReDrawFlag != tmp_flag) { break; }
		if (t >= key_delay)
		{
			ascent(i in 0 .. key_count)
			{
				if (GetVirtualKeyState(check_keys[i]) == KEY_HOLD) { key_value = check_keys[i]; break; }
			}
		}
		if (check_same)
		{
			if (t >= key_delay_short)
			{
				if (GetVirtualKeyState(before_key) == KEY_HOLD) { key_value = before_key; break; }
			}
			else if (GetVirtualKeyState(before_key) != KEY_HOLD)
			{
				// 離したらリセット
				check_same = false;
			}
		}
		ascent(i in 0 .. key_count)
		{
			if (GetVirtualKeyState(check_keys[i]) == KEY_PUSH) { key_value = check_keys[i]; break; }
		}
		t++;
		yield;
	}
	SetAreaCommonData(CAREA_TITLE, "MenuKey", key_value);
	if (check_same && key_value == before_key) { SetAreaCommonData(CAREA_TITLE, "MenuHold", before_hold+1); }
	else { SetAreaCommonData(CAREA_TITLE, "MenuHold", 0); }
	return(key_value);
}


//----------------------------------------------------
// txtファイルにログ出力
//----------------------------------------------------
function WriteLogText(text)
{
	if (!DEBUG_MODE) { return; }
	
	let path = GetModuleDirectory() ~ "debug_log.txt";
	
	if (length(text) > 0)
	{
		let lines = [];
		let objFile = ObjFile_Create(OBJ_FILE_TEXT);
		ObjFile_Open(objFile, path);
		ObjFile_OpenNW(objFile, path);
		ascent(iLine in 0 .. length(lines))
		{
			let line = ObjFileT_GetLineText(objFile, iLine+1);
			ObjFileT_AddLine(objFile, line);
		}
		ObjFileT_AddLine(objFile, text);
		ObjFile_Store(objFile);
		Obj_Delete(objFile);
	}
	else
	{
		let objFile = ObjFile_Create(OBJ_FILE_TEXT);
		ObjFile_OpenNW(objFile, path);
		ObjFile_Store(objFile);
		Obj_Delete(objFile);
	}
}

