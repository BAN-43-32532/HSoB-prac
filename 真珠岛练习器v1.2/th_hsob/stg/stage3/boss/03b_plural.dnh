//#東方弾幕風[Plural]
#ScriptVersion[3]
#Title["3面中ボス 連続再生"]
#Text[""]
#System["./../../../system/System.dnh"]


#include"./../../../lib/lib_Boss.dnh"
#include"./../../../lib/lib_ChapterSkip.dnh"
let objBossScene;
let objTalkScene02;
let dir = GetCurrentScriptDirectory();
let dest=GetStartChapter(3);
SetDivLoadEnabledPre(3);
let divload=GetDivLoadEnabled(3);
let boss_div1;
@Initialize
{
	TPlural();
}

@Event
{
	alternative( GetEventType() )
	//　ボス登場イベント
	case( ev_ResistBoss )
	{
		ResistSceneScript();
	}
}

@MainLoop
{
	yield;
}

// 裏でボスシーンをロード
task TPlural
{
	// ボスシーンを作成します。
	objBossScene = ObjEnemyBossScene_Create();
	
	// ボスシーンの初期設定
	BossScene_SetInit(objBossScene,
		BOSS_ID_HIKARIKO,	// ボスID
		[MaxX+32, -128],		// 初期座標
	);


	// ボスの攻撃
	
	let hpc=0;
	if(dest<=5){
		// 会話
		alternative(GetPlayerType())
		case(PLAYER_REIMU_A)  { ObjEnemyBossScene_Add(objBossScene, 0, dir ~ "03b_Talk01a.dnh"); }
		case(PLAYER_MARISA_A) { ObjEnemyBossScene_Add(objBossScene, 0, dir ~ "03b_Talk01b.dnh"); }
		case(PLAYER_SAKUYA_A) { ObjEnemyBossScene_Add(objBossScene, 0, dir ~ "03b_Talk01c.dnh"); }
		case(PLAYER_KAGUYA_A) { ObjEnemyBossScene_Add(objBossScene, 0, dir ~ "03b_Talk01d.dnh"); }
	}
	if(dest==13){
		dest=14;
	}
	if(divload){
		boss_div1=ObjEnemyBossScene_Create();
		BossScene_SetInit(boss_div1,
			BOSS_ID_HIKARIKO,	// ボスID
			[MaxX+32, -128],		// 初期座標
		);
		let sht=[0,0,1,1,2,2,3,3];
		let ht=[0,0,-1,-1,-2,-2,-3,-3];
		let st=["03b_Attack01.dnh","03b_Spell01.dnh",
			"03b_Attack02.dnh","03b_Spell02.dnh",
			"03b_Attack03.dnh","03b_Spell03.dnh",
			"03b_Talk_S04.dnh","03b_Spell04.dnh"];
		//dest:6~12
		ascent(i in 0..length(st)){
			if(i+6==dest){
				ObjEnemyBossScene_Add(boss_div1,0,dir ~ st[i]);
				ObjEnemyBossScene_LoadInThread(boss_div1);
				SetScriptState(3,dest);
			}
			if(i+6>dest){
				ObjEnemyBossScene_Add(objBossScene,sht[i]+ht[dest-6],dir ~ st[i]);
			}
		}
	}
	else{
		let sht=[0,0,1,1,2,2,3,3];
		let ht=[0,0,-1,-1,-2,-2,-3,-3];
		let st=["03b_Attack01.dnh","03b_Spell01.dnh",
			"03b_Attack02.dnh","03b_Spell02.dnh",
			"03b_Attack03.dnh","03b_Spell03.dnh",
			"03b_Talk_S04.dnh","03b_Spell04.dnh"];
		//dest:6~12
		//but card reaches 13
		let nd=max(dest,6);
		ascent(i in 0..length(st)){
			if(i+6>=nd){
				ObjEnemyBossScene_Add(objBossScene,sht[i]+ht[nd-6],dir ~ st[i]);
			}
		}
	}
	ObjEnemyBossScene_LoadInThread(objBossScene);
}

// ボスシーンの起動
task ResistSceneScript
{
//	if (!IsReplay()) { CDM_SetCharacterDataFlag(BOSS_ID_SETUJO); }	// 解説フラグON
	
	// ここでボスシーンを作成。（会話２）
	objTalkScene02 = ObjEnemyBossScene_Create();
	alternative(GetPlayerType())
	case(PLAYER_REIMU_A)  { ObjEnemyBossScene_Add(objTalkScene02, 0, dir ~ "03b_Talk02a.dnh"); }
	case(PLAYER_MARISA_A) { ObjEnemyBossScene_Add(objTalkScene02, 0, dir ~ "03b_Talk02b.dnh"); }
	case(PLAYER_SAKUYA_A) { ObjEnemyBossScene_Add(objTalkScene02, 0, dir ~ "03b_Talk02c.dnh"); }
	case(PLAYER_KAGUYA_A) { ObjEnemyBossScene_Add(objTalkScene02, 0, dir ~ "03b_Talk02d.dnh"); }
	ObjEnemyBossScene_LoadInThread(objTalkScene02);
	if(divload){
		////////
		// 魔法陣リセット
		BossEffect_ResetMagicCircle();
		
		// ボスシーン開始
		ObjEnemyBossScene_Regist(boss_div1);
		SetRawdel(true);
		//敵ボスシーンが終了するまで待機
		while(!Obj_IsDeleted(boss_div1))
		{
			yield;
		}
		SetRawdel(false);
		////////
	}
	// 魔法陣リセット
	BossEffect_ResetMagicCircle();
	
	// ボスシーン開始
	ObjEnemyBossScene_Regist(objBossScene);

	//敵ボスシーンが終了するまで待機
	while(!Obj_IsDeleted(objBossScene))
	{
		yield;
	}

	// 魔法陣リセット
	BossEffect_ResetMagicCircle();
	ChapterReset();	//チャプターリセット
	waitEx(15);
	
	// ラスワチェック
	if (GetLastSpellEnable() || GetStartChapter(3)>1)
	{
		// ラスワ呼び出しフラグ
		CallLastWord();
		
		// ボスシーンを作成します。
		let objLastWord = ObjEnemyBossScene_Create();
		ObjEnemyBossScene_Add(objLastWord, 0, dir ~ "03b_SpellEx01.dnh");
		ObjEnemyBossScene_LoadInThread(objLastWord);
		waitEx(150);
		ObjEnemyBossScene_Regist(objLastWord);

		// ラスワが終了するまで待機
		while(!Obj_IsDeleted(objLastWord))
		{
			yield;
		}
	}

	// 会話シーン２開始
	ObjEnemyBossScene_Regist(objTalkScene02);
	// 会話シーン２が終了するまで待機
	while(!Obj_IsDeleted(objTalkScene02))
	{
		yield;
	}
	
	// 連続再生終了通知
	FinalizePlural();

	//スクリプト終了
	CloseScript(GetOwnScriptID());
}
