#東方弾幕風[Single]
#ScriptVersion[3]
#Title["ひかり子道中2"]
#Text["ひかり子道中2"]
#Background[""]
#System["./../../../system/System.dnh"]

//---------------------------------------------------------------------

// 関数取り込み
#include"./../../../lib/lib_boss.dnh"
#include"./lib_Hikariko.dnh"

@Event {
	alternative(GetEventType())
	case(EV_REQUEST_LIFE)		{ SetScriptResult(180);	}
	case(EV_REQUEST_TIMER)		{ SetScriptResult(25);		}
	BossEvent();
}

@Initialize {
	//ダメージ透過率
	dIni_Shot = 10;
	// アイテム数
	bossDropItem = 50;
	// ふえーる残機
	bossDropItemSpecial = [MY_ITEM_LIFE];
	
	//初期設定
	Boss_Init();
	
	objEnemy = CreateBoss(BOSS_ID_HIKARIKO);	//ボスセット

	// チャプター開始
	ChapterStartBoss(GetBossID(objEnemy), -2, CHAPTER_BOSS);

	// 使い魔と体力連動
	CheckLinkLife_Boss(objEnemy);

	Task_Main();
}

@MainLoop {
	yield;
}

// メインタスク
task Task_Main
{
	if (GetPlayMode() == GAME_MAIN || GetPlayMode() == GAME_PRACTICE)
	{
		standBy(objEnemy, CenX, 170, 60);
		SetEnemyVisible(objEnemy, false);
		SetAction(objEnemy, ACT_SHOT2, 180);
		BossCharge(objEnemy);
		waitObj(objEnemy, 60);
		SetEnemyVisible(objEnemy, true);
	}
	else
	{
		SetAction(objEnemy, ACT_SHOT2, 180);
		BossCharge(objEnemy);
		standBy(objEnemy, CenX, 170, 60);
		waitObj(objEnemy, 60);
	}
	
	let way = 5;
	let wt = [300, 240, 240, 240][Difficult];
	let muki = rand_dir();

	let t = 0;
	while(eAlive)
	{
		if (t > 0) { SetAction(objEnemy, ACT_SHOT2, 120); }
		ascent(i in 0 .. way)
		{
			if (!eAlive) { return; }
			let sa = -90-(i-(way-1)/2)*15*muki+45*muki;
			let add_a = (0.5+0.025*i)*muki;
			let timer = 120;
			SetFamilier01(ObjMove_GetX(objEnemy)+32*cos(sa), ObjMove_GetY(objEnemy)+32*sin(sa),
				4, sa, add_a, timer);
			CallSound(sndShot2);
			waitObj(objEnemy, 15);
		}
		if (!eAlive) { return; }
		EnemyRandomMove(objEnemy, 160, 32);
		waitObj(objEnemy, wt-15*way);
		muki = -muki;
		t++;
	}
}

// 鳥ショット
task SetFamilier01(iniX, iniY, iniSpeed, iniAngle, iniValue, iniTimer)
{
	let iniMuki = [1, -1][iniValue < 0];
	
	let eSize = ENEMY_M;
	let eColor = EFF_RED;
	let baseAngle = iniAngle;
	
	let objFamilier = MakeEnemyFamilier(iniX, iniY, 100, eSize);
	CheckLinkLife_Familier(objEnemy, objFamilier, 0.8);	// ボスと体力連動
	DrawBirdOption(objFamilier, EFF_BLUE, iniAngle, 1);
	TAct();
	WaitForKilledFamilier(objFamilier, 8, 0);
	ZakoFinalize(objFamilier, eColor);
	
	task TAct()
	{
		TMove;
		TShot;
	}
	
	task TMove()
	{
		let add_a = iniValue;

		Enemy_SetPatternA2(objFamilier, 0, iniSpeed, iniAngle, -iniSpeed/(iniTimer-60), add_a, iniSpeed/4);
		Enemy_SetPatternA2(objFamilier, iniTimer-30, NO_CHANGE, NO_CHANGE, iniSpeed/120, add_a, iniSpeed);
		
		ascent(t in 0 .. iniTimer)
		{
			if (Obj_IsDeleted(objFamilier)) { return; }
			let tmp = GetCurve_z((t+1)/iniTimer);
			Enemy_SetAngle(objFamilier, Enemy_GetAngle(objFamilier)+(tmp*1.5)*iniMuki);
			waitObj(objFamilier, 1);
		}
		TCheckDispINOut(objFamilier, 32);
	}
	
	task TShot()
	{
		waitObj(objFamilier, 60);
		
		let speed = [1.33, 1.5, 2, 2][Difficult];
		let way = [3, 5, 5, 5][Difficult];
		let way_angle = [180, 90, 75, 75][Difficult];
		let graphic = BGW_FIRE_BLUE;
		let wt = [12, 10, 12, 12][Difficult];
		
		let t = 0;
		while(CanShotBullet(objFamilier))
		{
			let tmp = GetCurve_d(min(1, t/240));
			let sa = iniAngle + rand(-5,5);
			let tmp_wa = way_angle * (1+tmp*3)/4;
			CreateShotWayA2(ObjMove_GetX(objFamilier), ObjMove_GetY(objFamilier),
				0, sa, speed/120, speed, graphic, 30, way, tmp_wa, 0);
			CallSound(sndShot5);
			t += wt;
			waitObj(objFamilier, wt);
		}
	}
}
