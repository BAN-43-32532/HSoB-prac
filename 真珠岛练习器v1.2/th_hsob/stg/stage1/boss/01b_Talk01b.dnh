#東方弾幕風[Single]
#ScriptVersion[3]
#Title["1面　会話前半"]
#Text["1面　会話前半"]
#Background[""]
#System["./../../../system/System.dnh"]

//---------------------------------------------------------------------

// 関数取り込み
#include"./../../../lib/lib_boss.dnh"
#include"./../../../lib/lib_talk.dnh"

// 各ボスのオブジェクトID
let MOVE_END = false;

@Event {
	alternative(GetEventType())
	case(EV_REQUEST_LIFE)		{ SetScriptResult( 0);	}
	case(EV_REQUEST_TIMER)		{ SetScriptResult(-1);	}
	BossEvent();
}

@Initialize {
	Boss_Init();	//初期設定
	
	objEnemy  = CreateBossTalk(BOSS_ID_RANRAN);

	ChapterReset();	//チャプターリセット

	Task_Main();
}

@MainLoop {
	yield;

}

// メインタスク
task Task_Main()
{
	// 魔法陣リセット
	BossEffect_ResetMagicCircle();

	// 会話準備
	StartTalk();

	wait(120);

	//会話処理
	FuncTalk();
	
	//次のシーンへ
	FinishTalk(true);
	while(!MOVE_END) { yield; }
	Boss_Finalize(objEnemy, false, false);
}

// 会話処理
function FuncTalk()
{
	// オブジェクト作成
	let cutinBoss1   = CreateTalkObject(BOSS_ID_RANRAN, true);
	let cutinPlayer1 = CreateTalkObject(BOSS_ID_MARISA, false);
	
	let talkID = -1;
	loop
	{
		let talkType = FuncTalkSingle(talkID);
		if (talkType == 0) { break; }
		else if (talkType == 1) { talkID = max(0, talkID+WaitNext()); }
		else { talkID++; }
	}

	// 会話処理単発（巻き戻しも出来るように）
	function FuncTalkSingle(talkID)
	{
		let talk_type = 1;
		alternative(talkID)
		case(-1)
		{
			// 初期設定
			SetImageVisible(cutinBoss1, false);
			SetImageFoward(cutinPlayer1, true);
			ChangeTalkTexture(cutinPlayer1, I_WORRY);
			
			wait(15);	// 表示しきるまでウェイト
			talk_type = 2;	// キー入力待ちしない
		}
		case(0)
		{
			ChangeTalkTexture(cutinPlayer1, I_WORRY);
			SetTalkEx(cutinPlayer1, "行けども行けども白黒だ。[r]これは縁起が良くないぜ。");
		}
		case(1)
		{
			ChangeTalkTexture(cutinPlayer1, I_NOTICE);
			ChangeTalkTexture(cutinBoss1, I_SMILE2);
			SetTalkEx2(cutinBoss1, "ヒッヒッヒッ、[r]ならば私が紅く染めてやろうかね", -48, -280);
		}
		case(2)
		{
			EnterBoss();
			BossScene_SetNameVisible(objScene, true);	//ボスネーム表示状態
			SetImageVisible(cutinBoss1, true);	// ボス画像出現フラグ
			SetBossName(cutinBoss1, objEnemy);

			SetTalkEx(cutinBoss1, "緋々色染めはこうやって、[r]塗れば塗るほど色が変わって…");
		}
		case(3)
		{
			ChangeTalkTexture(cutinPlayer1, I_SMILE);
			SetTalkEx(cutinPlayer1, "おぉ、上手い！");
		}
		case(4)
		{
			ChangeTalkTexture(cutinBoss1, I_NORMAL);
			SetTalkEx(cutinBoss1, "お前さんも私の染めた紅の服を買い、[r]赤い魔法使いに転職してはどうだね？");
		}
		case(5)
		{
			ChangeTalkTexture(cutinBoss1, I_SMILE2);
			SetTalkEx(cutinBoss1, "攻撃・回復・補助と[r]術の幅が広くなりそうじゃろう？");
		}
		case(6)
		{
			ChangeTalkTexture(cutinPlayer1, I_UPSET);
			SetTalkEx(cutinPlayer1, "待て、それは全部中途半端で[r]打ち止めになる奴だ");
		}
		case(7)
		{
			ChangeTalkTexture(cutinPlayer1, I_NORMAL);
			ChangeTalkTexture(cutinBoss1, I_NORMAL);
			SetTalkEx(cutinBoss1, "気にするな！　今日は上人さまの御力で[r]霊気が漲っているので無料サービスだ！");

			PlayMusicWithName( 3, 30, 30 );
		}
		case(8)
		{
			ChangeTalkTexture(cutinBoss1, I_DOYA);
			SetTalkEx(cutinBoss1, "お前さんともども！[r]この白き世界を全て、[r]我が紅に染め抜いてくれよう！");
			SetFukidashiType(cutinBoss1, FUKIDASHI_ANGER);
		}
		others
		{
			return(0);
		}
		return(talk_type);
	}
}

// 登場モーション
task EnterBoss()
{
	if (Obj_GetValueD(objEnemy, "BOSS_MOVED", false)) { return; }
	Obj_SetValue(objEnemy, "BOSS_MOVED", true);

	// 登場モーション
	SetAction(objEnemy, ACT_MOVE, 600);
	ObjMove_SetDestAtWeightSpeedUp(objEnemy, CenX, 140, 10, 1, 0.05, 5);
	wait(90);
	MOVE_END = true;
}
