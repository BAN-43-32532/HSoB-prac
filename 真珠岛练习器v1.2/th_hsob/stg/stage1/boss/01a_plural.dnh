//#東方弾幕風[Plural]
#ScriptVersion[3]
#Title["1面中ボス 連続再生"]
#Text[""]
#System["./../../../system/System.dnh"]


#include"./../../../lib/lib_Boss.dnh"
#include"./../../../lib/lib_ChapterSkip.dnh"

let objBossScene;
let objTalkScene02;
let dir = GetCurrentScriptDirectory();

@Initialize
{
	TPlural();
}

@Event
{
	alternative( GetEventType() )
	//　ボス登場イベント
	case( ev_ResistBoss )
	{
		ResistSceneScript();
	}
}

@MainLoop
{
	yield;
}

// 裏でボスシーンをロード
task TPlural
{
	// ボスシーンを作成します。
	objBossScene = ObjEnemyBossScene_Create();
	
	// ボスシーンの初期設定
	BossScene_SetInit(objBossScene,
		BOSS_ID_RANRAN,	// ボスID
		[MaxX+32, -128],		// 初期座標
	);

	// ボスの攻撃
	let dest=GetStartChapter(1);
	if(dest<=3){
		ObjEnemyBossScene_Add(objBossScene, 0, dir ~ "01a_Attack01.dnh");
	}
	if(dest<=4){
		//其?是一定会add的，因?大于4就不?callboss了
		if (Difficult >= D_HARD) { ObjEnemyBossScene_Add(objBossScene, 0, dir ~ "01a_Spell01.dnh"); }
	}
	
	ObjEnemyBossScene_Add(objBossScene, 0, dir ~ "01a_Talk02a_dummy.dnh");
	ObjEnemyBossScene_LoadInThread(objBossScene);
}

// ボスシーンの起動
task ResistSceneScript
{
//	if (!IsReplay()) { CDM_SetCharacterDataFlag(BOSS_ID_SETUJO); }	// 解説フラグON
	
	// ここでボスシーンを作成。（会話２）
	objTalkScene02 = ObjEnemyBossScene_Create();
	ObjEnemyBossScene_Add(objTalkScene02, 0, dir ~ "01a_Talk02a_dummy.dnh");
	ObjEnemyBossScene_LoadInThread(objTalkScene02);

	// 魔法陣リセット
	BossEffect_ResetMagicCircle();
	
	// ボスシーン開始
	ObjEnemyBossScene_Regist(objBossScene);

	//敵ボスシーンが終了するまで待機
	while(!Obj_IsDeleted(objBossScene))
	{
		yield;
	}
	
	// 連続再生終了通知
	FinalizePlural();

	// 会話シーン２開始
	ObjEnemyBossScene_Regist(objTalkScene02);
	// 会話シーン２が終了するまで待機
	while(!Obj_IsDeleted(objTalkScene02))
	{
		yield;
	}

	//スクリプト終了
	CloseScript(GetOwnScriptID());
}
