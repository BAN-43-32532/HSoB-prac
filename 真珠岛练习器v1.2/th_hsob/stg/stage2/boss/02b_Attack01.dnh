#東方弾幕風[Single]
#ScriptVersion[3]
#Title["ちぃ通常1"]
#Text["ちぃ通常1"]
#Background[""]
#System["./../../../system/System.dnh"]

//---------------------------------------------------------------------

// 関数取り込み
#include"./../../../lib/lib_boss.dnh"
#include"./lib_Chii.dnh"

@Event {
	alternative(GetEventType())
	case(EV_REQUEST_LIFE)		{ SetScriptResult(2000);	}
	case(EV_REQUEST_TIMER)		{ SetScriptResult(35);		}
	BossEvent();
}

@Initialize {
	// アイテム数
	bossDropItem = 50;

	Boss_Init();	//初期設定
	objEnemy = CreateBoss(BOSS_ID_CHII);	//ボスセット

	// チャプター開始
	ChapterStartBoss(GetBossID(objEnemy), 1, CHAPTER_BOSS);

	Task_Main();
}

@MainLoop {
	yield;
}

// メインタスク
task Task_Main
{
	standBy(objEnemy, CenX, 160, 90);
	waitObj(objEnemy, 60);
	
	let wt = 210;
	
	let muki = rand_dir();
	let t = 0;
	while(eAlive)
	{
		CallSound(sndPower3);
		waitObj(objEnemy, 30);

		ascent(i in 0 .. 3)
		{
			if (!eAlive) { return; }
			let loop_count = 5;
			let oa_seed = rand_int(0,loop_count-1);
			let mpos = [[CenX+120*muki,180],[CenX-80*muki,140],[CenX+40*muki,160]][i];
			
			SetAction(objEnemy, ACT_MOVE, 65);
			MoveTo(objEnemy, mpos[0], mpos[1], 65);
			Shot02(70);
			waitObj(objEnemy, 65);
		}

		if (!eAlive) { return; }
		BossCharge(objEnemy);
		waitObj(objEnemy, 90);

		if (!eAlive) { return; }
		Shot01();
		waitObj(objEnemy, wt);
		
		muki = -muki;
		t++;
	}
}

// 自機外し
task Shot01()
{
	let speedMax =	[2.0	,2.5	,2.5	,3.0][Difficult];
	let speedMin =	[1.0	,1.25	,1.0	,0.75][Difficult];
	let way =		[4		,4		,6		,8][Difficult];
	let way_angle =	[60		,45		,30		,20][Difficult];;
	let count = 20;
	
	let base_angle = -90;

	SetAction(objEnemy, ACT_SHOT2, 120);
	ascent(t in 0 .. count)
	{
		if (!eAlive) { return; }
		let ss = rand(speedMin, speedMax);
		let wa = way_angle * (1-t/30/2);
		BlinkTumaShot_ToPlayer(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), rand(1, 6^2)^0.5, base_angle+rand(-90,90), BGW_STICK_YELLOW, 30,
			ss, 0, 60, way, wa, 15);
		CallSound(sndShot3);
		waitObj(objEnemy, 2);
	}
}

// 自機狙い
task Shot02(timer)
{
	let speed = [3,4,5,10][Difficult];
//	let timer = 60;
	let wt = [5,3,4,3][Difficult];
	let way = [1,1,3,5][Difficult];
	let w1 = (way-1)/2;
	let arc = [0,0,24,12][Difficult];
	
//	let base_angle = GetAngleToPlayer(objEnemy);
	
	ascent(t in 0 .. timer)
	{
		if (!eAlive) { return; }
		if (t%wt == 0)
		{
			ascent(iW in 0 ..way){
				let ss = speed;
				let sa = -90+rand(-30,30)+((ObjMove_GetX(objEnemy)-MinX)/GetClipWidth-0.5)*120;
				TumaShot_ToPlayer(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), rand(1, 5^2)^0.5, sa, BGW_STICK_RED, 30,
					ss, arc*(iW-w1), BGW_STICK_RED, 60);
				CallSound(sndShot3);
			}
		}
		waitObj(objEnemy, 1);
	}
}
