// -------------- 敵単体の処理 -------------- //

#include "./lib_MiniChii.dnh"

let S2_ZAKO_SPD_Y = 0.75;

// オープニング用人魂
task SetEnemyH01(iniX, iniY, iniSpeed, iniAngle, iniValue, iniWait)
{
	if (flgStopSetZako) { return; }
	
	let baseAngle = iniAngle;
	let muki = iniSpeed;
	
	let e_type = iniValue%2;
	let eSize = ENEMY_S;
	let eColor = EFF_BLUE;
	
	let objEnemy = MakeEnemy(iniX, iniY, 20, eSize);
	DrawHitodama( objEnemy, eColor );
	TAct();
	WaitForKilled(objEnemy);
	ZakoFinalize(objEnemy, eColor);
	
	task TAct()
	{
		SetHitodamaVisibleSilent(objEnemy, 30);
		TCheckDispINOut(objEnemy, 16);
		TShot();
		
		let bx = iniX;
		let by = iniY;
		baseAngle = iniAngle;
		let br = 0;
		
		let t = 0;
		while(CanShotBullet(objEnemy))
		{
			let tmp = (t+120)/120;
			let ex = bx + cos(baseAngle)*br;
			let ey = by + sin(baseAngle)*br;
			ObjMove_SetPosition(objEnemy, ex, ey);
			by += S2_ZAKO_SPD_Y;
			br += 0.5*tmp;
			baseAngle += 1/tmp*muki;
			t++;
			waitObj(objEnemy, 1);
		}
	}
	
	// ショット
	task TShot()
	{
		waitObj(objEnemy, 30);
		if (!CanShotBullet(objEnemy)) { return; }
		CallSound(sndShot2);
		waitObj(objEnemy, 30);

		let timer =	[16,	32,		48,		80][Difficult];
		let wt =	[4,		4,		4,		6][Difficult];
		
		let speed =	[1.75,	2.0,	2.25,	2.0][Difficult];
		let way =	[1,		1,		1,		2][Difficult];
		let way_angle = 45;
		let graphic = BGB_SCALE_BLUE;
		
		let t = 0;
		while(CanShotBullet(objEnemy) && t < timer)
		{
			let sa = baseAngle-90*muki;
			CreateShotWayA2(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy),
				0, sa, speed/120, speed, graphic, 15, way, way_angle, 0);
			waitObj(objEnemy, wt);
			t += wt;
		}
	}
}

// ビット陰陽玉
task SetEnemyI01(iniX, iniY, iniSpeed, iniAngle, iniValue, iniWait)
{
	if (flgStopSetZako) { return; }
	
	let e_type = iniValue%2;
	let eSize = ENEMY_M;
	let eColor = EFF_WHITE;
	
	let objEnemy = MakeEnemy(iniX, iniY, 50, eSize);
	SetEnemyDropItem(objEnemy, [MY_ITEM_TIME_1], [MY_ITEM_TIME_1, 4]);
	DrawOnmyou( objEnemy, eColor );
	TAct();
	WaitForKilled(objEnemy);
	
	if (CheckKillType(objEnemy) == 2)
	{
		// 撃ち返し
		TUchikaeshi();
	}
	ZakoFinalize(objEnemy, eColor);
	
	task TAct()
	{
		SetHitodamaVisibleSilent(objEnemy, 30);
		TCheckDispINOut(objEnemy, 16);
		TShot();
		
		let bx = iniX;
		let by = iniY;
		let ba = iniAngle;
		let br = iniValue;
		
		let t = 0;
		while(CanShotBullet(objEnemy))
		{
			let ex = bx + cos(ba)*br;
			let ey = by + sin(ba)*br;
			ObjMove_SetPosition(objEnemy, ex, ey);
			by += S2_ZAKO_SPD_Y;
			ba += iniSpeed;
			t++;
			waitObj(objEnemy, 1);
		}
		// ショット
		task TShot()
		{
//			let interval = [9999, 150, 75, 45][Difficult];
			let interval =	[9999,	200,	200,	180][Difficult];
			let way =		[0,		1,		3,		4][Difficult];
			let spd =		[0,		1.5,	1.8,	2.0][Difficult];
			let arc =		[0,		0,		45,		30][Difficult];
			let ss = 1;
			waitObj(objEnemy, interval - (iniWait%interval));
			while(CanShotBullet(objEnemy))
			{
//				ShotA(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 3, 3.00, 0, 3.0);
//				ShotA(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 3, 2.00, 180, 3.0);
				if(((ObjMove_GetX(objEnemy)-GetPlayerX())^2 + (ObjMove_GetY(objEnemy)-GetPlayerY())^2) > 60^2)
				{
					ShotB(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), way, spd*1.00, ba, arc);
					ShotB(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), way, spd*0.60, ba, arc);
				}
				waitObj(objEnemy, interval);
			}

			task ShotB(x, y, way, spd, ang, arc){
				let w1 = (way-1)/2;
				ascent(iW in 0 .. way){
					let a = ang + arc*(iW-w1);
					ascent(iQ in 0 .. 3){
						let s = spd*(1-0.025*iQ);
						My_CreateShotA2Ex(x, y, 0, a, s/60, s, BGB_SCALE_RED, 15, DELAY_ADD);
					}
				}
			}

			task ShotA(x, y, way, spd, ang, arc){
				let Array = [];
				ascent(i in 1 .. way+1){
					Array = Array ~ [i];
				}
				descent(i in 1 .. way){
					Array = Array ~ [i];
				}
				ascent(iQ in 0 .. length(Array)){
					let w1 = (Array[iQ]-1)/2;
					ascent(iW in 0 .. Array[iQ]){
						let vx = 120 - arc*0.75*iQ;
						let vy = arc * (iW-w1);
						let l = ((vx^2 + vy^2)^0.5);
						let a = ba + atan2(vy, vx)+ang;
						let s = spd*l/120;
						My_CreateShotA2Ex(x, y, 0, a, s/60, s, BGB_SCALE_RED, 15, DELAY_ADD);
					}
				}
			}
		}
	}
	

	task TUchikaeshi()
	{
		let count = 2;
		
		let ba = GetAngleToPlayer(objEnemy);
		ascent(i in 0 .. count)
		{
			let sa = ba+180 + rand(-75,75);
			let ss = rand(1, 4);
			let timer = 60 + i*15;
			SetEnemyH02(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), ss, sa, timer, 0);
		}
	}
}

// 破裂人魂
task SetEnemyH02(iniX, iniY, iniSpeed, iniAngle, iniValue, iniWait)
{
	if (flgStopSetZako) { return; }
	
	let e_type = iniValue%2;
	let eSize = ENEMY_S;
	let eColor = EFF_RED;
	
	let objEnemy = MakeEnemy(iniX, iniY, 10, eSize);
	SetEnemyDropItem(objEnemy, [MY_ITEM_TIME_2], [MY_ITEM_TIME_1, 0]);
	DrawHitodama( objEnemy, eColor );
	TAct();
	WaitForKilled(objEnemy);	
	if (CheckKillType(objEnemy) == 2)
	{
		// 撃ち返し
		TCounterShot();
	}
	ZakoFinalize(objEnemy, eColor);
	
	task TAct()
	{
		let to_speed = 3;
		
		SetHitodamaVisible(objEnemy, iniValue);
		TCheckDispINOut(objEnemy, 16);
		Enemy_SetPatternA2(objEnemy, 0, iniSpeed, iniAngle, -iniSpeed/60, 0, 0);
		Enemy_SetPatternA4(objEnemy, iniValue, 0, 0, to_speed/120, 0, to_speed, objPlayer);
		TShot();
	}
	
	
	// ショット
	task TShot()
	{
		waitObj(objEnemy, iniValue);
	}

	task TCounterShot()
	{
		if(((ObjMove_GetX(objEnemy)-GetPlayerX())^2 + (ObjMove_GetY(objEnemy)-GetPlayerY())^2) <= 96^2){return;}
		let speed = [1.25, 1.25, 1.5, 1.5][Difficult];
		let way = [1, 1, 1, 3][Difficult];
		let graphic = [BGW_BALL_BS_RED, BGW_BALL_BS_RED, BGW_BALL_M_RED, BGW_BALL_M_RED][Difficult];
		let w1 = (way-1)/2;
		let ba = GetAngleToPlayer(objEnemy);
		ascent(iW in 0 .. way)
		{
			let objS = My_CreateShotA2Ex(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), -speed, ba+60*(iW-w1), speed/60, speed, graphic, 30, DELAY_ADD);
			ObjMove_AddPatternA2(objS, 60, NO_CHANGE, ba-45*(iW-w1), speed/60, 0, speed);
		}
	}
}

// チラ見せちぃちぃ
task SetEnemyChii01(iniX, iniY, iniSpeed, iniAngle, iniValue, iniWait)
{
	if (flgStopSetZako) { return; }
	
	let e_type = iniValue%2;
	let eSize = ENEMY_M;
	let eColor = EFF_BLUE;
	
	let objEnemy = MakeEnemy(MaxX+32, MinY+150, 10000, eSize);
	BossAnime_Initialize(objEnemy);
	SetEnemyVisible(objEnemy, false);
	TAct();
	WaitForKilled(objEnemy);
	ZakoFinalize(objEnemy, eColor);
	
	task TAct()
	{
		MoveTo(objEnemy, iniX+32, iniY-64, 60);
		SetAction(objEnemy, ACT_MOVE, 60);
		waitObj(objEnemy, 60);
		
		MoveTo(objEnemy, iniX, iniY, 30);
		SetAction(objEnemy, ACT_MOVE, 30);
		waitObj(objEnemy, 105);
		
		Enemy_SetPatternA2(objEnemy, 0, 0, -90-55, 4/30, 0, 4);
		SetAction(objEnemy, ACT_MOVE_L, 240);
		TCheckDispINOut(objEnemy, 64);
		
		TShot();
		ascent(t in 0 .. 150)
		{
			if (Obj_IsDeleted(objEnemy)) { break; }
			Obj_SetValue(objEnemy, "ANIME_SC", cos(t/150*90));
			waitObj(objEnemy, 1);
		}
	}
	
	// ショット
	task TShot()
	{
		let wt = 7;
		let timer = 85;
		
		let muki = -1;
		let t = 0;
		while(CanShotBullet(objEnemy) && t < timer)
		{
			let sa = 90 + 90*muki;
			let ss = ((t+1)^2*1.777)%4.75;
			SetEnemyChii02(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), ss, sa, 1, 90);
			waitObj(objEnemy, wt);
			muki = -muki;
			t += wt;
		}
	}
}


// ばらまきちぃちぃ
task SetEnemyChii02(iniX, iniY, iniSpeed, iniAngle, iniValue, iniWait)
{
	if (flgStopSetZako) { return; }
	
	let e_type = iniValue%2;
	let eSize = ENEMY_M;
	let eColor = EFF_BLUE;
	
	let objEnemy = MakeEnemy(iniX, iniY, 30, eSize);
	BossAnime_Initialize(objEnemy);
	EnterMiniChii(objEnemy, 0.5);
	TAct();
	WaitForKilled(objEnemy);
	ZakoFinalize(objEnemy, eColor);
	
	task TAct()
	{
		Enemy_SetPatternA2(objEnemy, 0, iniSpeed, iniAngle, -iniSpeed/60, 0, 0);
		TShot();
		waitObj(objEnemy, 180);
		if (!CanShotBullet(objEnemy)) { return; }
		if (iniValue)
		{
			Enemy_SetPatternA4(objEnemy, 0, 0, 0, 2/60, 0, 2, objPlayer);
			SetAction(objEnemy, [ACT_MOVE_L, ACT_MOVE_R][ObjMove_GetX(objEnemy) < GetPlayerX], 9999);
			TCheckDispINOut(objEnemy, 32);
		}
		else
		{
			Enemy_SetPatternA2(objEnemy, 0, 0, iniAngle, 2/60, 0, 2);
			SetAction(objEnemy, [ACT_MOVE_L, ACT_MOVE_R][cos(iniAngle)>0], 9999);
			TCheckDispINOut(objEnemy, 32);
		}
	}
	
	// ショット
	task TShot()
	{
		waitObj(objEnemy, 105);

		let wt = 1;
		let timer = [3,		5,	10,		15][Difficult];
		let speed = [1.75,	2,	2.25,	1.5][Difficult];
		let angle = iniWait;
		
		SetAction(objEnemy, ACT_SHOT1, 60);
		
		let t = 0;
		while(CanShotBullet(objEnemy) && t < timer)
		{
			let ss = rand(1,3);
			let sa = GetAngleToPlayer(objEnemy)+180 + rand(-90,90);
			TumaShot(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy),
				ss, sa, BGW_STICK_YELLOW, 15, speed*rand(0.5,1), angle+rand(-15,15), BGW_STICK_YELLOW, 60);
			CallSound(sndShot3);
			waitObj(objEnemy, wt);
			t += wt;
		}
	}
}

// 自機狙いちぃちぃ
task SetEnemyChii03(iniX, iniY, iniSpeed, iniAngle, iniValue, iniWait)
{
	if (flgStopSetZako) { return; }
	
	let e_type = iniValue%2;
	let eSize = ENEMY_M;
	let eColor = EFF_BLUE;
	
	let objEnemy = MakeEnemy(iniX, iniY, 30, eSize);
	BossAnime_Initialize(objEnemy);
	EnterMiniChii(objEnemy, 0.5);
	TAct();
	WaitForKilled(objEnemy);
	ZakoFinalize(objEnemy, eColor);
	
	task TAct()
	{
		Enemy_SetPatternA2(objEnemy, 0, iniSpeed, iniAngle, -iniSpeed/60, 0, 0);
		TShot();
		waitObj(objEnemy, 180);
		if (!CanShotBullet(objEnemy)) { return; }
		if (iniValue)
		{
			Enemy_SetPatternA4(objEnemy, 0, 0, 0, 2/60, 0, 2, objPlayer);
			SetAction(objEnemy, [ACT_MOVE_L, ACT_MOVE_R][ObjMove_GetX(objEnemy) < GetPlayerX], 9999);
			TCheckDispINOut(objEnemy, 32);
		}
		else
		{
			Enemy_SetPatternA2(objEnemy, 0, 0, iniAngle, 2/60, 0, 2);
			SetAction(objEnemy, [ACT_MOVE_L, ACT_MOVE_R][cos(iniAngle)>0], 9999);
			TCheckDispINOut(objEnemy, 32);
		}
	}
	
	// ショット
	task TShot()
	{
		waitObj(objEnemy, 105);

		let wt = 1;
		let timer = [3, 6, 12, 12][Difficult];
		let speed = [2, 3, 2.25, 1.5][Difficult];
		let way = [1, 1, 1, 3][Difficult];
		let rnd = [0, 0, 2, 2][Difficult];
		let w1 = ((way-1)/2);
		let L = ((ObjMove_GetX(objEnemy)-GetPlayerX())^2 + (ObjMove_GetY(objEnemy)-GetPlayerY())^2)^0.5;
		let arc = 30*240/L;
		SetAction(objEnemy, ACT_SHOT1, 60);
		
		let t = 0;
		while(CanShotBullet(objEnemy) && t < timer)
		{
			let ss = rand(1,3);
			let sa = GetAngleToPlayer(objEnemy) + rand(-90,90);
			ascent(i in 0 .. way){
				TumaShot_ToPlayer(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy),
				ss, sa, BGW_STICK_RED, 15, speed, arc*(i-w1)+rand(-rnd, rnd), BGW_STICK_RED, 60);
			}	
			CallSound(sndShot3);
			waitObj(objEnemy, wt);
			t += wt;
		}
	}
	
	function AngleAdjustment(angle, accuracy){
		if(accuracy == 0){return angle;}
		return trunc(angle/accuracy+0.5)*accuracy;
	}
}

// 大きめちぃちぃ
task SetEnemyChii04(iniX, iniY, iniSpeed, iniAngle, iniValue, iniWait)
{
	if (flgStopSetZako) { return; }
	
	let e_type = iniValue%2;
	let eSize = ENEMY_L;
	let eColor = EFF_BLUE;
	
	let objEnemy = MakeEnemy(iniX, iniY, 250, eSize);
	BossAnime_Initialize(objEnemy);
	EnterMiniChii(objEnemy, 0.8);
	TAct();
	WaitForKilled(objEnemy);
	ZakoFinalize(objEnemy, eColor);
	
	task TAct()
	{
		Enemy_SetPatternA1(objEnemy, 0, iniSpeed, iniAngle);
		SetAction(objEnemy, [ACT_MOVE_L, ACT_MOVE_R][cos(iniAngle)>0], 9999);
		TCheckDispINOut(objEnemy, 32);

		TShot();
	}
	
	// ショット
	task TShot()
	{
		waitObj(objEnemy, 90);

		let wt = 	[15,	10,		8,		6][Difficult];
		let timer = [300,	300,	300,	300][Difficult];
		let speed = [2,		1.5,	2.25,	1.5][Difficult];
		let way = 3;
		let way2 = 	[1,		1,		2,		3][Difficult];
		let w2 = ((way2-1)/2);
		let muki = [1, -1][cos(iniAngle)>0];
		
		let ba = GetAngleToPlayer(objEnemy);
		
		let st = 0;
		let t = 0;
		while(CanShotBullet(objEnemy) && t < timer)
		{
			ascent(iW in 0 .. way)
			{
				ascent(iW2 in 0 .. way2)
				{
					let ss = 3;
					let sa = ba + iW*360/way + 10*st*muki + 1.2*(iW2-w2);
					TumaShot(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy),
						ss, sa, BGW_STICK_YELLOW, 15, speed, sa-90*muki, BGW_STICK_YELLOW, 60);
				}
			}
			CallSound(sndShot3);
			waitObj(objEnemy, wt);
			t += wt;
			st++;
		}
	}
}


task TumaShot(x, y, speed, angle, graphic, delay, to_speed, to_angle, to_graphic, timer)
{
	let obj = My_CreateShotA2(x, y, speed, angle, -speed/timer, 0, graphic, delay);
	wait(timer);
	if (!Obj_IsDeleted(obj))
	{
		let move_angle = (to_angle-angle)%360;
		if (move_angle > 180) { move_angle -= 360; }
		
		let to_obj = My_CreateShotA1(ObjMove_GetX(obj), ObjMove_GetY(obj), 0, angle, to_graphic, 0);
		ObjMove_AddPatternA2(to_obj, 60, NO_CHANGE, NO_CHANGE, to_speed/120, 0, to_speed);
		ObjShot_FadeDelete(obj);
		
		CallSound(sndShot2);

		ascent(t in 0 .. 30)
		{
			if (Obj_IsDeleted(to_obj)) { break; }
			let tmp = GetCurve_ad((t+1)/30);
			ObjRender_SetAlpha(to_obj, tmp*512);
			ObjMove_SetAngle(to_obj, angle+move_angle*tmp);
			ObjMove_SetAngle(obj, angle+move_angle*tmp);
			wait(1);
		}
	}
}
task TumaShot_ToPlayer(x, y, speed, angle, graphic, delay, to_speed, to_offset_angle, to_graphic, timer)
{
	let obj = My_CreateShotA2(x, y, speed, angle, -speed/timer, 0, graphic, delay);
	wait(timer);
	if (!Obj_IsDeleted(obj))
	{
		let to_angle = GetAngleToPlayer(obj) + to_offset_angle;
		let move_angle = (to_angle-angle)%360;
		if (move_angle > 180) { move_angle -= 360; }
		
		let to_obj = My_CreateShotA1(ObjMove_GetX(obj), ObjMove_GetY(obj), 0, angle, to_graphic, 0);
		ObjMove_AddPatternA2(to_obj, 60, NO_CHANGE, NO_CHANGE, to_speed/120, 0, to_speed);
		ObjShot_FadeDelete(obj);
		
		CallSound(sndShot2);

		ascent(t in 0 .. 45)
		{
			if (Obj_IsDeleted(to_obj)) { break; }
			let tmp = GetCurve_ad((t+1)/45);
			ObjRender_SetAlpha(to_obj, tmp*512);
			ObjMove_SetAngle(to_obj, angle+move_angle*tmp);
			ObjMove_SetAngle(obj, angle+move_angle*tmp);
			wait(1);
		}
	}
}
