//#東方弾幕風[Plural]
#ScriptVersion[3]
#Title["6面 連続再生"]
#Text[""]
#System["./../../../system/System.dnh"]


#include"./../../../lib/lib_Boss.dnh"
#include"./../../../lib/lib_ChapterSkip.dnh"
#include"./../lib_World06_BackGround01a.dnh"

let objBossScene;
let objTalkScene02;
let dir = GetCurrentScriptDirectory();

let dest=GetStartChapter(6);
let boss_div1;
SetDivLoadEnabledPre(6);
let divload=GetDivLoadEnabled(6);
@Initialize
{
	TPlural();
}

@Event
{
	alternative( GetEventType() )
	//　ボス登場イベント
	case( ev_ResistBoss2 )
	{
		ResistSceneScript();
	}
}

@MainLoop
{
	yield;
}

// 裏でボスシーンをロード（めっちゃウェイトかけつつ）
task TPlural
{
	// ボスシーンを作成します。
	objBossScene = ObjEnemyBossScene_Create();
	
	// ボスシーンの初期設定
	BossScene_SetInit(objBossScene,
		BOSS_ID_HOROU,	// ボスID
		[MaxX+32, -128],		// 初期座標
	);
	if(dest<=6){
		// 会話
		alternative(GetPlayerType())
		case(PLAYER_REIMU_A)  { ObjEnemyBossScene_Add(objBossScene, 0, dir ~ "06b_Talk01a.dnh"); }
		case(PLAYER_MARISA_A) { ObjEnemyBossScene_Add(objBossScene, 0, dir ~ "06b_Talk01b.dnh"); }
		case(PLAYER_SAKUYA_A) { ObjEnemyBossScene_Add(objBossScene, 0, dir ~ "06b_Talk01c.dnh"); }
		case(PLAYER_KAGUYA_A) { ObjEnemyBossScene_Add(objBossScene, 0, dir ~ "06b_Talk01d.dnh"); }
	}
	if(divload){
		boss_div1=ObjEnemyBossScene_Create();
		BossScene_SetInit(boss_div1,
			BOSS_ID_HOROU,	// ボスID
			[MaxX+32, -128],		// 初期座標
		);
		let sht=[0,0,1,1,2,2,3,3,4,4,5,6];
		let ht=[0,0,-1,-1,-2,-2,-3,-3,-4,-4,-5,-6];
		let st=["06b_Attack01.dnh","06b_Spell01.dnh",
			"06b_Attack02.dnh","06b_Spell02.dnh",
			"06b_Attack03.dnh","06b_Spell03.dnh",
			"06b_Attack04.dnh","06b_Spell04.dnh",
			"06b_Attack05.dnh","06b_Spell05.dnh",
			"06b_Spell06.dnh","06b_Spell07.dnh"];
		//dest:7~18
		ascent(i in 0..length(st)){
			if(i+7==dest){
				ObjEnemyBossScene_Add(boss_div1,0,dir ~ st[i]);
				ObjEnemyBossScene_LoadInThread(boss_div1);
				SetScriptState(6,dest);
			}
			else if(i+7>dest){
				ObjEnemyBossScene_Add(objBossScene,sht[i]+ht[dest-7+1],dir ~ st[i]);
			}
		}
	}
	else{
		let sht=[0,0,1,1,2,2,3,3,4,4,5,6];
		let ht=[0,0,-1,-1,-2,-2,-3,-3,-4,-4,-5,-6];
		let st=["06b_Attack01.dnh","06b_Spell01.dnh",
			"06b_Attack02.dnh","06b_Spell02.dnh",
			"06b_Attack03.dnh","06b_Spell03.dnh",
			"06b_Attack04.dnh","06b_Spell04.dnh",
			"06b_Attack05.dnh","06b_Spell05.dnh",
			"06b_Spell06.dnh","06b_Spell07.dnh"];
		//dest:7~18
		let nd=max(dest,7);
		ascent(i in 0..length(st)){
			if(i+7>=nd){
				ObjEnemyBossScene_Add(objBossScene,sht[i]+ht[nd-7],dir ~ st[i]);
			}
		}
	}
	ObjEnemyBossScene_LoadInThread(objBossScene);
}

// ボスシーンの起動
task ResistSceneScript
{
	if(divload){
		////////
		// 魔法陣リセット
		BossEffect_ResetMagicCircle();
		
		// ボスシーン開始
		ObjEnemyBossScene_Regist(boss_div1);
		//SetRawDelEx(boss_div1);
		SetRawdel(true);
		//敵ボスシーンが終了するまで待機
		while(!Obj_IsDeleted(boss_div1))
		{
			yield;
		}
		
		////////
	}
	// 魔法陣リセット
	BossEffect_ResetMagicCircle();
	
	// ボスシーン開始
	ObjEnemyBossScene_Regist(objBossScene);
	SetRawdel(false);
	//敵ボスシーンが終了するまで待機
	while(!Obj_IsDeleted(objBossScene))
	{
		yield;
	}

	// 連続再生終了通知
	FinalizePlural();

	// 魔法陣リセット
	ChapterReset();	//チャプターリセット
	waitEx(15);
	
	// ラスワチェック
	if (GetLastSpellEnable())
	{
		// フラグONにするだけ
		AddResult(GetSpellCardIndex(GetSpellCardIDEx(1007, Difficult)),
			false, 0, 0);
	}

	//スクリプト終了
	CloseScript(GetOwnScriptID());
}
