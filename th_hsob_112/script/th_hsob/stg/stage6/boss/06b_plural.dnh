//#東方弾幕風[Plural]
#ScriptVersion[3]
#Title["6面 連続再生"]
#Text[""]
#System["./../../../system/System.dnh"]


#include"./../../../lib/lib_Boss.dnh"
#include"./../lib_World06_BackGround01a.dnh"

let objBossScene;
let objTalkScene02;
let dir = GetCurrentScriptDirectory();

@Initialize
{
	TPlural();
}

@Event
{
	alternative( GetEventType() )
	//　ボス登場イベント
	case( ev_ResistBoss2 )
	{
		ResistSceneScript();
	}
}

@MainLoop
{
	yield;
}

// 裏でボスシーンをロード（めっちゃウェイトかけつつ）
task TPlural
{
	// ボスシーンを作成します。
	objBossScene = ObjEnemyBossScene_Create();
	
	// ボスシーンの初期設定
	BossScene_SetInit(objBossScene,
		BOSS_ID_HOROU,	// ボスID
		[MaxX+32, -128],		// 初期座標
	);

	// 会話
	alternative(GetPlayerType())
	case(PLAYER_REIMU_A)  { ObjEnemyBossScene_Add(objBossScene, 0, dir ~ "06b_Talk01a.dnh"); }
	case(PLAYER_MARISA_A) { ObjEnemyBossScene_Add(objBossScene, 0, dir ~ "06b_Talk01b.dnh"); }
	case(PLAYER_SAKUYA_A) { ObjEnemyBossScene_Add(objBossScene, 0, dir ~ "06b_Talk01c.dnh"); }
	case(PLAYER_KAGUYA_A) { ObjEnemyBossScene_Add(objBossScene, 0, dir ~ "06b_Talk01d.dnh"); }

	// ボスの攻撃
	ObjEnemyBossScene_Add(objBossScene, 0, dir ~ "06b_Attack01.dnh");
	ObjEnemyBossScene_Add(objBossScene, 0, dir ~ "06b_Spell01.dnh");
	ObjEnemyBossScene_Add(objBossScene, 1, dir ~ "06b_Attack02.dnh");
	ObjEnemyBossScene_Add(objBossScene, 1, dir ~ "06b_Spell02.dnh");
	ObjEnemyBossScene_Add(objBossScene, 2, dir ~ "06b_Attack03.dnh");
	ObjEnemyBossScene_Add(objBossScene, 2, dir ~ "06b_Spell03.dnh");
	ObjEnemyBossScene_Add(objBossScene, 3, dir ~ "06b_Attack04.dnh");
	ObjEnemyBossScene_Add(objBossScene, 3, dir ~ "06b_Spell04.dnh");
	ObjEnemyBossScene_Add(objBossScene, 4, dir ~ "06b_Attack05.dnh");
	ObjEnemyBossScene_Add(objBossScene, 4, dir ~ "06b_Spell05.dnh");
	ObjEnemyBossScene_Add(objBossScene, 5, dir ~ "06b_Spell06.dnh");
	ObjEnemyBossScene_Add(objBossScene, 6, dir ~ "06b_Spell07.dnh");
	ObjEnemyBossScene_LoadInThread(objBossScene);
}

// ボスシーンの起動
task ResistSceneScript
{
	// 魔法陣リセット
	BossEffect_ResetMagicCircle();
	
	// ボスシーン開始
	ObjEnemyBossScene_Regist(objBossScene);

	//敵ボスシーンが終了するまで待機
	while(!Obj_IsDeleted(objBossScene))
	{
		yield;
	}

	// 連続再生終了通知
	FinalizePlural();

	// 魔法陣リセット
	ChapterReset();	//チャプターリセット
	waitEx(15);
	
	// ラスワチェック
	if (GetLastSpellEnable())
	{
		// フラグONにするだけ
		AddResult(GetSpellCardIndex(GetSpellCardIDEx(1007, Difficult)),
			false, 0, 0);
	}

	//スクリプト終了
	CloseScript(GetOwnScriptID());
}
