#東方弾幕風[Single]
#ScriptVersion[3]
#Title["ﾎﾛｳﾁｬﾝ通常3"]
#Text["ﾎﾛｳﾁｬﾝ通常3"]
#Background[""]
#System["./../../../system/System.dnh"]

//---------------------------------------------------------------------

// 関数取り込み
#include"./../../../lib/lib_boss.dnh"
#include"./lib_Horou.dnh"

@Event {
	alternative(GetEventType())
	case(EV_REQUEST_LIFE)		{ SetScriptResult(5777);	}
	case(EV_REQUEST_TIMER)		{ SetScriptResult(70);		}
	BossEvent();
}

@Initialize {
	// アイテム数
	bossDropItem = 50;
	//ダメージ透過率
	dIni_Shot = 225;
	// 停止無効
	bossResistTime = 1;

	Boss_Init();	//初期設定
	objEnemy = CreateBoss(BOSS_ID_HOROU);	//ボスセット

	// チャプター開始
	ChapterStartBoss(GetBossID(objEnemy), 3, CHAPTER_BOSS);
	
	InitHorou();

	// 使い魔と体力連動
	CheckLinkLife_Boss(objEnemy);

	Task_Main();
	
	SetBackGroundBase( objEnemy, 2 );
}

@MainLoop {
	yield;
}


// メインタスク
task Task_Main
{
	// オプション配置
	SetEffect06_Opt(objEnemy, 0, 0);
	objHorouEffect = SetEffect06(objEnemy);

	StandBy_TimeStop(objEnemy, CenX, 160, 210);
	waitObj(objEnemy, 60);
	HorouReigekiSynchro(WEAPON_PURPLE, 180);

	TPenaShot();
	
	let wt = [165, 150, 120, 90][Difficult];
	let muki = 1;
	
	let t = 0;
	while(eAlive)
	{
		TShot_sub(muki);
		EnemyRandomMove(objEnemy, 160, 32);
		waitObj(objEnemy, wt);
		muki = -muki;
		t++;
	}
	
	task TShot_sub(muki)
	{
		let speed = [1, 1, 1.25, 1.5][Difficult];
		let angle = rand(0, 360);
		let way = [9, 10, 12, 15][Difficult];
		let way_angle_max = [120, 120, 150, 150][Difficult];
		let wt = [5, 5, 5, 5][Difficult];
		let wt2 = [10, 10, 5, 5][Difficult];
		let graphic = BGW_WING_PURPLE;
		
		ascent(i in 0 .. 5)
		{
			if (!eAlive) { return; }
			let sr = 120-i*15;
			let way_angle = way_angle_max - i*5;
			let tmp_way = way;
			let ss = speed * (1+i/5);
			ascent(j in 0 .. 6)
			{
				let sa = angle + j*60;
				let sx = ObjMove_GetX(objEnemy) + 32*cos(sa);
				let sy = ObjMove_GetY(objEnemy) + 32*sin(sa);
				WingShotWay(sx, sy, ss, sa, graphic, 30, 60+i*wt2, tmp_way, way_angle/tmp_way*muki, sr);
			}
			CallSound(sndShot2);
			waitObj(objEnemy, wt);
		}
	}
}

task TPenaShot()
{
	let array_pos = [2, 4];
	let way = 4;
	let r = 0;
	
	let t = 0;
	while(eAlive)
	{
		if (CheckInPlayer(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 72))
		{
			let ax = array_pos[0];
			let ay = array_pos[1] + t%way;
			let sa = ObjRender_GetAngleZ(objHorouEffect[ax][ay]);
			let sx = ObjRender_GetX(objHorouEffect[ax][ay]) + r*cos(sa);
			let sy = ObjRender_GetY(objHorouEffect[ax][ay]) + r*sin(sa);
			let speed = 16;
			HorouLaserA02(sx, sy, speed, sa, 20, 24, BGW_LIGHT_L_PURPLE, 60);
		}
		t++;
		waitObj(objEnemy, 10);
	}
}

task TLaser01(muki)
{
	let array_pos;
	let way;
	let r;
	if (muki > 0)
	{
		// 右側
		array_pos = [2, 4];
		way = 4;
		r = 0;
	}
	else
	{
		// 左側
		array_pos = [1, 2];
		way = 6;
		r = 48;
	}
	ascent(i in 0 .. way)
	{
		if (!eAlive) { return; }
		let ax = array_pos[0];
		let ay = array_pos[1] + i;
		let sa = ObjRender_GetAngleZ(objHorouEffect[ax][ay]);
		let sx = ObjRender_GetX(objHorouEffect[ax][ay]) + r*cos(sa);
		let sy = ObjRender_GetY(objHorouEffect[ax][ay]) + r*sin(sa);
		let speed = 16;
		HorouLaserA02(sx, sy, speed, sa, 20, 24, BGW_LIGHT_L_RED, 60);
		waitObj(objEnemy, 5);
	}
}

task HorouLaserA02(x, y, speed, angle, l_len, l_wid, graphic, delay)
{
	CallSound(sndBulletBreak);
	
	let vs = 6;
	let va = angle;
	let obj = CreateHorouLaser(x, y, vs, va, l_len, l_wid, graphic, delay);
	ObjMove_AddPatternA2(obj, 0, vs, NO_CHANGE, -vs/30, 0, 0);
	ObjMove_AddPatternA4(obj, 30, 0, 0, speed/30, 0, speed, objPlayer, NO_CHANGE);
	wait(30);
	if (!Obj_IsDeleted(obj)) { CallSound(sndBulletBreak); }
}


task WingShotWay(x, y, speed, angle, graphic, delay, timer, way, way_angle, r)
{
	ascent(i in 0 .. way)
	{
		let add_a = (i-(way-1)/2)*way_angle;
		let to_r = (0.25 + 0.75*(i+1)/way) * r;
		let to_offset = add_a*1;
		WingShot(x, y, speed, angle, graphic, delay,
			timer, add_a, to_r, to_offset);
	}
}

task WingShot(x, y, speed, angle, graphic, delay, timer, add_a, to_r, to_offset)
{
	let obj = My_CreateShotA1(x, y, 0, angle, graphic, delay);
// omoi
//	WingAddShot(obj);
	ascent(t in 0 .. timer)
	{
		if (Obj_IsDeleted(obj)) { return; }
		let tmpd = GetCurve_d((t+1)/timer);
		let tmpa = GetCurve_ad((t+1)/timer);
		let br = tmpd * to_r;
		let ba = angle + add_a*tmpa;
		let sx = x + br*cos(ba);
		let sy = y + br*sin(ba);
		let sa = ba + to_offset*tmpd;
		ObjMove_SetPosition(obj, sx, sy);
		ObjMove_SetAngle(obj, sa);
		yield;
	}
	if (Obj_IsDeleted(obj)) { return; }
	ObjMove_AddPatternA2(obj, 0, NO_CHANGE, NO_CHANGE, speed/120, 0, speed);
	CallSound(sndShot2);
}

task WingAddShot(objBase)
{
	let obj = My_CreateShotA1(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 0, 0, ADD_BGW_FUGU_WHITE, 0);
	let col = GetEffectColor(BULLET_EFFECT[ObjShot_GetImageID(objBase)%8]);
	ObjRender_SetColor(obj, col[0]*0.66+64, col[1]*0.66+64, col[2]*0.66+64);
	SetShotDisabled(obj);
	let t = 0;
	while(!Obj_IsDeleted(objBase))
	{
		if (Obj_IsDeleted(obj)) { return; }
		if (t < 60)
		{
			let tmp = GetCurve((t+1)/60);
			ObjRender_SetAlpha(obj, tmp*255);
		}
		let sa = ObjMove_GetAngle(objBase);
		ObjMove_SetPosition(obj, ObjMove_GetX(objBase), ObjMove_GetY(objBase));
		ObjMove_SetAngle(obj, sa);
		t++;
		yield;
	}
	Obj_Delete(obj);
}
