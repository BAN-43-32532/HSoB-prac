#東方弾幕風[Single]
#ScriptVersion[3]
#Title["通常1"]
#Text["通常1"]
#Background[""]
#System["./../../../system/System.dnh"]

//---------------------------------------------------------------------

// 関数取り込み
#include"./../../../lib/lib_boss.dnh"
#include"./lib_Mumei.dnh"

@Event {
	alternative(GetEventType())
	case(EV_REQUEST_LIFE)		{ SetScriptResult(3000);	}
	case(EV_REQUEST_TIMER)		{ SetScriptResult(60);		}
	BossEvent();
}

@Initialize {

	// アイテム数
	if (IsRainbow())	{ bossDropItem = 150; }
	else			{ bossDropItem = 50; }

	//防御率再計算
	SetMumeiDefence();

	Boss_Init();	//初期設定
	objEnemy = CreateBoss(BOSS_ID_MUMEI);	//ボスセット

	InitMumei();

	// チャプター開始
	ChapterStartBoss(GetBossID(objEnemy), 1, CHAPTER_BOSS);

	// 使い魔と体力連動
	CheckLinkLife_Boss(objEnemy);

	Task_Main();
}

@MainLoop {
	yield;
}



// メインタスク
task Task_Main
{
	standBy(objEnemy, CenX, 120, 60);
	waitObj(objEnemy, 60);

	BossCharge(objEnemy);
	waitObj(objEnemy, 60);
	Shot1();
	
	task Shot1()
	{
		let wt = [135, 150, 135][DifficultEx()];
		let D = -1;
		
		let count = 0;
		while(eAlive)
		{
			SetAction(objEnemy, ACT_SHOT2, 120);
			DishShotSet(D);
			CallSound(sndBoon);
			D = -D;
			count++;
			if (count%2 == 0) { D = -D; waitObj(objEnemy, wt/2); }
			waitObj(objEnemy, wt-60);
			if (!eAlive) { return; }
			EnemyRandomMove(objEnemy, 120, 64);
			waitObj(objEnemy, 60);
		}
	}
}

task DishShotSet(Dirc)
{
	let Dish_Way = [5, 3, 5][DifficultEx()];
	let Dish_WayAngle = [30, 45, 30][DifficultEx()];
	let Dish_SpdMin = 1;
	let B_ThrowR = 0;
	let B_ThrowAng = AngleAdjustment(GetAngleToPlayer(objEnemy), 90);
	let B_ThrowSpd = 0;
	let B_DishCt = 0;
	let angAdd = 1;
	let throwFlag = false;
	ascent(i in 0 .. Dish_Way){
		DishShot(Dish_WayAngle*(i-(Dish_Way-1)/2));
	}
	ascent(i in 1 .. 61){
		B_ThrowR = 75*sin(90*i/60);
		angAdd += 4/60;
		B_ThrowAng += angAdd*Dirc;
		B_ThrowSpd = angAdd*3.14159265359/180*B_ThrowR;
		yield;
	}
	let dx = GetPlayerX() - ObjMove_GetX(objEnemy);
	let dy = GetPlayerY() - ObjMove_GetY(objEnemy);
	let PL = (dy^2 + dx^2)^0.5;
	let PA = atan2(dy, dx);
//---------------------------例のバグ------------------------------------				
	let fin_a = PA-acos(B_ThrowR/max(B_ThrowR, PL))*Dirc;
//	let fin_a = PA-acos(B_ThrowR/PL)*Dirc;
//-----------------------------------------------------------------------				
	fin_a = AngleAdjustment(fin_a, 90);
	if(Dirc * (fin_a-B_ThrowAng) <= 0){fin_a += 360*Dirc;}
	while(Dirc * (fin_a-B_ThrowAng) > 0){
		angAdd += 4/60;
		B_ThrowAng += angAdd*Dirc;
		B_ThrowSpd = angAdd*3.14159265359/180*B_ThrowR;
		yield;
	}
	throwFlag = true;
	B_ThrowAng = fin_a;
	if (eAlive && B_DishCt > 0) { CallSound(sndShot7); }

	// お皿単品
	task DishShot(B_AngAdd)
	{
		let eSize = 28;
		let eLife = 80;
		let objS = PorcelainEnemy(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), PORCELAIN_DISH, eSize, 60, eLife, 0.8);
		let objImgAll = Obj_GetValueD(objS, "IMAGE_ID_ALL", []);
		B_DishCt++;
		TMain();
		
		task TMain()
		{
			TMove();
			TShot();
			while(!Obj_IsDeleted(objS)) { yield; }
			B_DishCt--;
		}
		task TMove()
		{
			let BX = ObjMove_GetX(objEnemy) + B_ThrowR*cos(B_ThrowAng+B_AngAdd);
			let BY = ObjMove_GetY(objEnemy) + B_ThrowR*sin(B_ThrowAng+B_AngAdd);
			let BAng = 0;
			while(!throwFlag && !Obj_IsDeleted(objS)){
				BX = ObjMove_GetX(objEnemy) + B_ThrowR*cos(B_ThrowAng+B_AngAdd);
				BY = ObjMove_GetY(objEnemy) + B_ThrowR*sin(B_ThrowAng+B_AngAdd);
				BAng += -13*Dirc;
				ObjMove_SetDestAtFrame(objS, BX, BY, 1);
				yield;
			}
			BX = ObjMove_GetX(objEnemy) + B_ThrowR*cos(B_ThrowAng+B_AngAdd);
			BY = ObjMove_GetY(objEnemy) + B_ThrowR*sin(B_ThrowAng+B_AngAdd);
			let a = B_ThrowAng + B_AngAdd + 90*Dirc;
			let s = B_ThrowSpd;
			let sMinus = (s-Dish_SpdMin)/30;
			while(!Obj_IsDeleted(objS)){
				s = max(s-sMinus, Dish_SpdMin);
				BX += s*cos(a);
				BY += s*sin(a);
				BAng += -13*Dirc;
				ObjMove_SetDestAtFrame(objS, BX, BY, 1);
				yield;
			}
		}
		task TShot()
		{
			while(!throwFlag && !Obj_IsDeleted(objS))
			{
				yield;
			}
			
			let speed = [1.5, 1.33, 1.5][DifficultEx()];
			let timer = [240, 240, 240][DifficultEx()];
			let way = [6, 6, 6][DifficultEx()];
			let sub_way = [2, 2, 2][DifficultEx()];
			let wt = [15, 21, 15][DifficultEx()];
			let quant = 2;
			let add_angle = Dirc*1;
			
			let t = 0;
			let st = 0;
			while(!Obj_IsDeleted(objS) && t < timer)
			{
				let base_angle = B_ThrowAng + B_AngAdd + 180 + t*add_angle;
			//	let base_angle = GetAngleToPlayer(objS);
				ascent(i in 0 .. sub_way)
				{
					let ss = speed;
					let sa = base_angle + (i-(sub_way-1)/2)*360/way;
					RhombusShotEx(objEnemy, quant, 8.0, 4.8, ObjMove_GetX(objS)+36*cos(sa), ObjMove_GetY(objS)+36*sin(sa),
						ss, sa, BGW_CHRYSTAL_S_WHITE, 10);
				}
				CallSound(sndShot5);
				t += wt;
				st++;
			//	sub_way = max(1, sub_way-1);
				wait(wt);
			}
		}
	}

	function AngleAdjustment(ang, CenterAng){
		return ((ang + CenterAng)%360) - CenterAng;
	}
}
