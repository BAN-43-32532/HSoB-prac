#東方弾幕風[Single]
#ScriptVersion[3]
#Title["4面　道中会話後半"]
#Text["4面　道中会話後半"]
#Background[""]
#System["./../../../system/System.dnh"]

//---------------------------------------------------------------------

// 関数取り込み
#include"./../../../lib/lib_boss.dnh"
#include"./../../../lib/lib_talk.dnh"
#include"./lib_Siran.dnh"

// 各ボスのオブジェクトID
let MOVE_END = false;

@Event {
	alternative(GetEventType())
	case(EV_REQUEST_LIFE)		{ SetScriptResult( 0);	}
	case(EV_REQUEST_TIMER)		{ SetScriptResult(-1);	}
	BossEvent();
}

@Initialize {
	Boss_Init();	//初期設定
	
	objEnemy  = CreateBossTalk(BOSS_ID_SIRAN);
	SetSiranRosary();	// 数珠配置

	ChapterReset();	//チャプターリセット

	Task_Main();
}

@MainLoop {
	yield;

}

// メインタスク
task Task_Main()
{
	// 魔法陣リセット
	BossEffect_ResetMagicCircle();

	// 会話準備
	StartTalk();

	wait(60);

	//会話処理
	FuncTalk();
	
	//次のシーンへ
	FinishTalk(true);
}

// 会話処理
function FuncTalk()
{
	// オブジェクト作成
	let cutinBoss1   = CreateTalkObject(BOSS_ID_SIRAN, true);
	let cutinPlayer1   = CreateTalkObject(BOSS_ID_SAKUYA, false);
	
	let talkID = -1;
	loop
	{
		let talkType = FuncTalkSingle(talkID);
		if (talkType == 0) { break; }
		else if (talkType == 1) { talkID = max(0, talkID+WaitNextTimer(1)); }
		else { talkID++; }
	}

	// 会話処理単発（巻き戻しも出来るように）
	function FuncTalkSingle(talkID)
	{
		let talk_type = 1;
		alternative(talkID)
		case(-1)
		{
			// 初期設定
			SetImageFoward(cutinBoss1, true);
			ChangeTalkTexture(cutinPlayer1, I_NORMAL);
			ChangeTalkTexture(cutinBoss1, I_UPSET);
			
			wait(15);	// 表示しきるまでウェイト
			talk_type = 2;	// キー入力待ちしない
		}
		case(0)
		{
			ChangeTalkTexture(cutinBoss1, I_UPSET);
			SetTalkEx(cutinBoss1, "これはいかんぞい。サエズッターで[r]新たなスペルを仕入れなければ");
			WaitNextTimer(150);
		}
		case(1)
		{
			ChangeTalkTexture(cutinPlayer1, I_DOYA2);
			SetTalkEx(cutinPlayer1, "私に止められないスペルなど、[r]この地上に少ししか無い");
			EnterBoss();
			WaitNextTimer(150);
		}
		others
		{
			return(0);
		}
		return(talk_type);
	}
}

// にげるよ
task EnterBoss()
{
	if (Obj_GetValueD(objEnemy, "BOSS_MOVED", false)) { return; }
	Obj_SetValue(objEnemy, "BOSS_MOVED", true);

	wait(60);
	if (!GetAreaCommonData(CAREA_STAGE, "BeforeTimeout", true))
	{
		// しっかり倒していたならボムあげる
		SetItem(ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), MY_ITEM_SPELL);
	}

	SetAction(objEnemy, ACT_MOVE, 600);
	MoveTo(objEnemy, CenX+64, -128, 75);
	wait(120);
	Boss_Finalize(objEnemy, false, false);
}
