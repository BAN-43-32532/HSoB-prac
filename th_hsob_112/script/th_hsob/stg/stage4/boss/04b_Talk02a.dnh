#東方弾幕風[Single]
#ScriptVersion[3]
#Title["4面　ボス会話後半"]
#Text["4面　ボス会話後半"]
#Background[""]
#System["./../../../system/System.dnh"]

//---------------------------------------------------------------------

// 関数取り込み
#include"./../../../lib/lib_boss.dnh"
#include"./../../../lib/lib_talk.dnh"
#include"./lib_Siran.dnh"

// 各ボスのオブジェクトID
let MOVE_END = false;

@Event {
	alternative(GetEventType())
	case(EV_REQUEST_LIFE)		{ SetScriptResult( 0);	}
	case(EV_REQUEST_TIMER)		{ SetScriptResult(-1);	}
	BossEvent();
}

@Initialize {
	Boss_Init();	//初期設定
	
	objEnemy  = CreateBossTalk(BOSS_ID_SIRAN);
	Obj_SetVisible(GetBossImageID(objEnemy), false);	//画像非表示

	ChapterReset();	//チャプターリセット

	Task_Main();
}

@MainLoop {
	yield;

}

// メインタスク
task Task_Main()
{
	// 魔法陣リセット
	BossEffect_ResetMagicCircle();

	// 会話準備
	StartTalk();

	wait(120);

	//会話処理
	FuncTalk();
	
	//次のシーンへ
	FinishTalk(true);
	Boss_Finalize(objEnemy, false, false);
}

// 会話処理
function FuncTalk()
{
	// オブジェクト作成
	let cutinBoss1   = CreateTalkObject(BOSS_ID_SIRAN, true);
	let cutinPlayer1   = CreateTalkObject(BOSS_ID_REIMU, false);
	
	let talkID = -1;
	loop
	{
		let talkType = FuncTalkSingle(talkID);
		if (talkType == 0) { break; }
		else if (talkType == 1) { talkID = max(0, talkID+WaitNext()); }
		else { talkID++; }
	}

	// 会話処理単発（巻き戻しも出来るように）
	function FuncTalkSingle(talkID)
	{
		let talk_type = 1;
		alternative(talkID)
		case(-1)
		{
			// 初期設定
			SetImageFoward(cutinBoss1, true);
			ChangeTalkTexture(cutinPlayer1, I_NORMAL2);
			ChangeTalkTexture(cutinBoss1, I_BORO1);
			
			wait(15);	// 表示しきるまでウェイト
			talk_type = 2;	// キー入力待ちしない
		}
		case(0)
		{
			SetTalkEx(cutinBoss1, "拙僧、ほどなく浄土に帰るなり。[r]この世に言い置くこと、一言も無しぞい");
		}
		case(1)
		{
			ChangeTalkTexture(cutinPlayer1, I_UPSET);
			SetTalkEx(cutinPlayer1, "いやいや、負けたんだから[r]時間止めの解除をさ");
		}
		case(2)
		{
			SetTalkEx(cutinBoss1, "あなやー巫女に翼をもがれて[r]飛べんぞい。");
		}
		case(3)
		{
			SetTalkEx(cutinBoss1, "これではあやつの元へ案内したくても[r]できんぞいー（棒読み）");
		}
		case(4)
		{
			ChangeTalkTexture(cutinPlayer1, I_ANGER_SMILE2);
			SetTalkEx(cutinPlayer1, "サエズッターで呼びつけなよ。[r]あんたがやらないなら私に通信機を寄越せ");
		}
		case(5)
		{
			SetTalkEx(cutinBoss1, "い、いかんぞい！");
		}
		case(6)
		{
			SetTalkEx(cutinBoss1, "拙僧の無断転サエの数々がばれ[r]ては、築き上げてきた地位が！");
		}
		case(7)
		{
			SetTalkEx(cutinBoss1, "た、頼むぞい。持って行くなら[r]せめて拙僧のサエズリの消去を…");
		}
		case(8)
		{
			ChangeTalkTexture(cutinPlayer1, I_ANGER_SMILE);
			SetTalkEx(cutinPlayer1, "沢山あるじゃん、[r]この世に言い置くこと");
		}
		others
		{
			return(0);
		}
		return(talk_type);
	}
}

// 登場モーション
task EnterBoss()
{
	if (Obj_GetValueD(objEnemy, "BOSS_MOVED", false)) { return; }
	Obj_SetValue(objEnemy, "BOSS_MOVED", true);

	// 登場モーション
	SetAction(objEnemy, ACT_MOVE, 600);
	ObjMove_SetDestAtWeightSpeedUp(objEnemy, CenX, 140, 10, 1, 0.05, 5);
	wait(90);
	MOVE_END = true;
}
