#東方弾幕風[Plural]
#ScriptVersion[3]
#Title["5面ボス 連続再生"]
#Text[""]
#System["./../../../system/System.dnh"]


#include"./../../../lib/lib_Boss.dnh"

let objBossScene;
let objTalkScene02;
let dir = GetCurrentScriptDirectory();

@Initialize
{
	TPlural();
}

@Event
{
	alternative( GetEventType() )
	//　ボス登場イベント
	case( ev_ResistBoss2 )
	{
		ResistSceneScript();
	}
}

@MainLoop
{
	yield;
}

// 裏でボスシーンをロード
task TPlural
{
	// ボスシーンを作成します。
	objBossScene = ObjEnemyBossScene_Create();
	
	// ボスシーンの初期設定
	BossScene_SetInit(objBossScene,
		BOSS_ID_SET,	// ボスID
		[MaxX+32, -128],		// 初期座標
	);

	// ボスの攻撃

	alternative(GetPlayerType())
	
	case(PLAYER_REIMU_A)  { ObjEnemyBossScene_Add(objBossScene, 0, dir ~ "05c_Talk01a.dnh"); }
	case(PLAYER_MARISA_A) { ObjEnemyBossScene_Add(objBossScene, 0, dir ~ "05c_Talk01b.dnh"); }
	case(PLAYER_SAKUYA_A) { ObjEnemyBossScene_Add(objBossScene, 0, dir ~ "05c_Talk01c.dnh"); }
	case(PLAYER_KAGUYA_A) { ObjEnemyBossScene_Add(objBossScene, 0, dir ~ "05c_Talk01d.dnh"); }
	
	ObjEnemyBossScene_Add(objBossScene, 0, dir ~ "05c_Attack01.dnh");
	ObjEnemyBossScene_Add(objBossScene, 0, dir ~ "05c_Spell01.dnh");
	ObjEnemyBossScene_Add(objBossScene, 1, dir ~ "05c_Attack02.dnh");
	ObjEnemyBossScene_Add(objBossScene, 1, dir ~ "05c_Spell02.dnh");
	ObjEnemyBossScene_Add(objBossScene, 2, dir ~ "05c_Attack03.dnh");
	ObjEnemyBossScene_Add(objBossScene, 2, dir ~ "05c_Spell03.dnh");
	ObjEnemyBossScene_Add(objBossScene, 3, dir ~ "05c_Attack04.dnh");
	ObjEnemyBossScene_Add(objBossScene, 3, dir ~ "05c_Spell04.dnh");
	ObjEnemyBossScene_Add(objBossScene, 4, dir ~ "05c_Spell05.dnh");
	
	ObjEnemyBossScene_LoadInThread(objBossScene);
}

// ボスシーンの起動
task ResistSceneScript
{
//	if (!IsReplay()) { CDM_SetCharacterDataFlag(BOSS_ID_SETUJO); }	// 解説フラグON
	
	// ここでボスシーンを作成。（会話２）
	objTalkScene02 = ObjEnemyBossScene_Create();
	alternative(GetPlayerType())
	case(PLAYER_REIMU_A)  { ObjEnemyBossScene_Add(objTalkScene02, 0, dir ~ "05c_Talk02a.dnh"); }
	case(PLAYER_MARISA_A) { ObjEnemyBossScene_Add(objTalkScene02, 0, dir ~ "05c_Talk02b.dnh"); }
	case(PLAYER_SAKUYA_A) { ObjEnemyBossScene_Add(objTalkScene02, 0, dir ~ "05c_Talk02c.dnh"); }
	case(PLAYER_KAGUYA_A) { ObjEnemyBossScene_Add(objTalkScene02, 0, dir ~ "05c_Talk02d.dnh"); }
	ObjEnemyBossScene_LoadInThread(objTalkScene02);

	// 魔法陣リセット
	BossEffect_ResetMagicCircle();
	
	// ボスシーン開始
	ObjEnemyBossScene_Regist(objBossScene);

	//敵ボスシーンが終了するまで待機
	while(!Obj_IsDeleted(objBossScene))
	{
		yield;
	}

	// 魔法陣リセット
	BossEffect_ResetMagicCircle();
	ChapterReset();	//チャプターリセット
	waitEx(15);
	
	// ラスワチェック
	if (GetLastSpellEnable())
	{
		// みすねべのラスワフラグもONにする
		AddResult(GetSpellCardIndex(GetSpellCardIDEx(1005, Difficult)),
			false, 0, 0);
		
		// ラスワ呼び出しフラグ
		CallLastWord();
		
		// ボスシーンを作成します。
		let objLastWord = ObjEnemyBossScene_Create();
		ObjEnemyBossScene_Add(objLastWord, 0, dir ~ "05c_SpellEx01.dnh");
		ObjEnemyBossScene_LoadInThread(objLastWord);
		waitEx(150);
		ObjEnemyBossScene_Regist(objLastWord);

		// ラスワが終了するまで待機
		while(!Obj_IsDeleted(objLastWord))
		{
			yield;
		}
	}


	// 会話シーン２開始
	ObjEnemyBossScene_Regist(objTalkScene02);
	// 会話シーン２が終了するまで待機
	while(!Obj_IsDeleted(objTalkScene02))
	{
		yield;
	}

	// 連続再生終了通知
	FinalizePlural();

	//スクリプト終了
	CloseScript(GetOwnScriptID());
}
