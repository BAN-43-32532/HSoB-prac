#東方弾幕風[Single]
#ScriptVersion[3]
#Title["1面　会話前半"]
#Text["1面　会話前半"]
#Background[""]
#System["./../../../system/System.dnh"]

//---------------------------------------------------------------------

// 関数取り込み
#include"./../../../lib/lib_boss.dnh"
#include"./../../../lib/lib_talk.dnh"

// 各ボスのオブジェクトID
let MOVE_END = false;

@Event {
	alternative(GetEventType())
	case(EV_REQUEST_LIFE)		{ SetScriptResult( 0);	}
	case(EV_REQUEST_TIMER)		{ SetScriptResult(-1);	}
	BossEvent();
}

@Initialize {
	Boss_Init();	//初期設定
	
	objEnemy  = CreateBossTalk(BOSS_ID_RANRAN);

	ChapterReset();	//チャプターリセット

	Task_Main();
}

@MainLoop {
	yield;

}

// メインタスク
task Task_Main()
{
	// 魔法陣リセット
	BossEffect_ResetMagicCircle();

	// 会話準備
	StartTalk();

	wait(120);

	//会話処理
	FuncTalk();
	
	//次のシーンへ
	FinishTalk(true);
	while(!MOVE_END) { yield; }
	Boss_Finalize(objEnemy, false, false);
}

// 会話処理
function FuncTalk()
{
	// オブジェクト作成
	let cutinBoss1   = CreateTalkObject(BOSS_ID_RANRAN, true);
	let cutinPlayer1 = CreateTalkObject(BOSS_ID_KAGUYA, false);
	
	let talkID = -1;
	loop
	{
		let talkType = FuncTalkSingle(talkID);
		if (talkType == 0) { break; }
		else if (talkType == 1) { talkID = max(0, talkID+WaitNext()); }
		else { talkID++; }
	}

	// 会話処理単発（巻き戻しも出来るように）
	function FuncTalkSingle(talkID)
	{
		let talk_type = 1;
		alternative(talkID)
		case(-1)
		{
			// 初期設定
			SetImageVisible(cutinBoss1, false);
			SetImageFoward(cutinPlayer1, true);
			ChangeTalkTexture(cutinPlayer1, I_SMILE);
			
			wait(15);	// 表示しきるまでウェイト
			talk_type = 2;	// キー入力待ちしない
		}
		case(0)
		{
			ChangeTalkTexture(cutinPlayer1, I_SMILE);
			SetTalkEx(cutinPlayer1, "ふふふ、永遠と須臾を操る私には[r]時間の操作なんて効かないわ。");
		}
		case(1)
		{
			ChangeTalkTexture(cutinPlayer1, I_SMILE3);
			SetTalkEx(cutinPlayer1, "さあ永琳が居ない内に憧れの[r]異変解決に出発よ！　ランラン〜♪");
		}
		case(2)
		{
			ChangeTalkTexture(cutinPlayer1, I_NOTICE);
			SetTalkEx2(cutinBoss1, "ヒッヒッヒッ、[r]私の名を呼ぶのは誰だね？", -48, -280);
		}
		case(3)
		{
			EnterBoss();
			BossScene_SetNameVisible(objScene, true);	//ボスネーム表示状態
			SetImageVisible(cutinBoss1, true);	// ボス画像出現フラグ
			SetBossName(cutinBoss1, objEnemy);

			ChangeTalkTexture(cutinPlayer1, I_NORMAL);
			ChangeTalkTexture(cutinBoss1, I_NORMAL);
			SetTalkEx(cutinBoss1, "高貴な姫君がお忍びで依頼とは、[r]我が緋々色染めもかくも有名になったか！");
		}
		case(4)
		{
			ChangeTalkTexture(cutinPlayer1, I_QUESTION);
			SetTalkEx(cutinPlayer1, "まあ緋々色金？[r]いつから染料に使えるほどありふれた存在に…");
		}
		case(5)
		{
			ChangeTalkTexture(cutinBoss1, I_DOYA2);
			SetTalkEx2(cutinBoss1, "ヒヒッ、実際は茜や紅花の染料だが、どうせ[r]見る目の貧しい箱入り娘には分からんだろう", 0, -32);
			SetFukidashiType(cutinBoss1, FUKIDASHI_THINK);
		}
		case(6)
		{
			ChangeTalkTexture(cutinPlayer1, I_WORRY2);
			SetTalkEx(cutinPlayer1, "はっ、よく考えれば地上では[r]生み出せない緋々色金を操る敵。");
		}
		case(7)
		{
			ChangeTalkTexture(cutinPlayer1, I_SURPRISE);
			SetTalkEx(cutinPlayer1, "そして時の停滞を呼び、変化を拒絶する。[r]もしやこの異変は、月の――！");
		}
		case(8)
		{
			ChangeTalkTexture(cutinBoss1, I_WORRY);
			SetTalkEx(cutinBoss1, "ヒッ、なぜスペルカードを構える！？");
		}
		case(9)
		{
			ChangeTalkTexture(cutinPlayer1, I_NORMAL);
			ChangeTalkTexture(cutinBoss1, I_DOYA);
			SetTalkEx(cutinBoss1, "そうかバレてしまったのでは仕方ない。");

			PlayMusicWithName( 3, 30, 30 );
		}
		case(10)
		{
			ChangeTalkTexture(cutinBoss1, I_NORMAL2);
			SetTalkEx(cutinBoss1, "では伝説の緋々色金より鮮やかな、[r]我が緋々色染めの神髄を見せてくれよう！");
			SetFukidashiType(cutinBoss1, FUKIDASHI_ANGER);
		}
		others
		{
			return(0);
		}
		return(talk_type);
	}
}

// 登場モーション
task EnterBoss()
{
	if (Obj_GetValueD(objEnemy, "BOSS_MOVED", false)) { return; }
	Obj_SetValue(objEnemy, "BOSS_MOVED", true);

	// 登場モーション
	SetAction(objEnemy, ACT_MOVE, 600);
	ObjMove_SetDestAtWeightSpeedUp(objEnemy, CenX, 140, 10, 1, 0.05, 5);
	wait(90);
	MOVE_END = true;
}
