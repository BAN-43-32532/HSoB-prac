// 初回解説
function ExtraFirstMessage()
{
	if (GetAreaCommonData(CAREA_SAVE, "EXTRA_FIRST", false)) { return; }
	
	// 暗転
	let objBlackBack = EffectBlackOutEx(30, 224, 90);
	
	// メッセージ
	let pos = [64, 64];
	waitEx(15);
	ExtraMessageSingle(pos, imgTtExtra02, [0, 0, 512, 256]);
//	ExtraMessageSingle(pos, imgTtExtra02, [0, 256, 512, 512]);
	ExtraMessageSingle(pos, imgTtExtra02, [0, 512, 512, 1024]);
	CallSoundPackage(sndTtCancel);
	
	Obj_SetValue(objBlackBack, "ERASE", true);	// ブラックアウト解除

	// コモンデータ保存
	SetAreaCommonData(CAREA_SAVE, "EXTRA_FIRST", true);
	SaveCommonDataAreaA2(CAREA_SAVE, saveCommonSAVE);
}

// メッセージ単体
function ExtraMessageSingle(pos, imgFile, rect)
{
	let IsEnd = false;
	CallSoundPackage(sndTtPause);

	let objText = ObjPrim_Create(OBJ_SPRITE_2D);
	Obj_SetRenderPriorityI(objText, 91);
	ObjPrim_SetTexture(objText, imgFile);
	ObjSprite2D_SetDestRect(objText, 0, 0, rect[2]-rect[0], rect[3]-rect[1]);
	ObjSprite2D_SetSourceRect(objText, rect[0], rect[1], rect[2], rect[3]);
	ObjRender_SetPosition(objText, pos[0], pos[1], 0);
	_ExtraMessageSingle_Draw(objText);
	
	// キー待ち
	WaitForResetKeyEx([VK_OK, VK_CANCEL]);
	waitEx(30);
	let downKey = CheckKey_MenuEx(30, 6, [VK_OK, VK_CANCEL]);
	IsEnd = true;
	
	task _ExtraMessageSingle_Draw(objText)
	{
		let t = 0;
		while(!IsEnd)
		{
			if (t < 30)
			{
				let tmp = (t+1)/15;
				ObjRender_SetAlpha(objText, tmp*255);
				t++;
			}
			yield;
		}
		descent(t in 0 .. 15)
		{
			let tmp = (t+1)/15;
			ObjRender_SetAlpha(objText, tmp*255);
			yield;
		}
		Obj_Delete(objText);
	}
}


